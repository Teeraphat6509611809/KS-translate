#### MONDAY

#**************************************
label th_NOP1:
window hide None

scene black
with None

scene bg op_snowywoods
show snow
with Dissolve(2.0)

window show

play music music_serene fadein 2.0

"ลมโชยพัดให้กิ่งไม้เสียดสีกันดังเป็นจังหวะ"

"ที่นี่เป็นที่ที่คู่รักมักมาอยู่ด้วยกันในหน้าร้อน ทั้งต้นไม้ที่ผลัดใบ ใบไม้เขียวขจี และปลอดคน"

"แต่เมื่อเป็นช่วงปลายหน้าหนาวอย่างตอนนี้ก็ไม่ต่างอะไรกับการยืนอยู่ใต้กิ่งไม้แห้ง ๆ"

"ระหว่างรอก็คอยเป่าลมอังแล้วถูมือเพื่อไม่ให้ประสาทชาท่ามกลางอากาศหนาวเย็นไปเสียก่อน"

hi "ต้องรอนานแค่ไหนเนี่ย เหมือนในกระดาษก็บอกว่าสี่โมงนะ"

"ใช่… กระดาษโน้ต… ที่แอบมาแทรกตัวอยู่ในหนังสือคณิต"

"ถ้าพูดถึงวิธีอะไรที่มันเชย ๆ จริง ๆ ชอบแบบที่เป็นจดหมายในตู้ล็อกเกอร์มากกว่านะ แต่อย่างน้อยวิธีนี้ก็ทำให้เห็นถึงความตั้งใจดี"

"หิมะค่อย ๆ ทับถมหนาขึ้นระหว่างที่กำลังคิดถึงกระดาษโน้ตนั้น"

"สิ่งบอกกระแสเวลาที่ไหลผ่านเดียวในโลกอันเอื่อยเฉื่อยนี้คือเกล็ดหิมะที่ร่วงหล่นลงมาจากฟากฟ้าสีขาว"

"ความอ้อยอิ่งของมันทำให้ดูเหมือนว่าเวลาช้าลง"

play sound sfx_rustling

"เสียงย่ำหิมะที่ทำลายความเงียบทำให้ฉันตกใจ มีบางอย่างกำลังมาจากข้างหลัง"

mystery "ฮิ… ฮิซาโอะ มาด้วยเหรอ"

"เป็นคำถามอันแผ่วเบาที่ดูอึกอัก"

"แต่ฉันจำเสียงอันบอบบางนั้นได้ทันที"

"ใจฉันเต้นไม่เป็นส่ำ"

"เป็นเสียงที่ได้ยินมาแล้วหลายร้อยครั้ง แต่ก็เพียงในฐานะคนแอบฟังเท่านั้น"

"ฉันหันไปทางเสียงในฝันนั้น ทำเอาใจเริ่มเต้นแรง"

window hide

$ ksgallery_unlock("evul other_iwanako")
scene ev other_iwanako_start
show snow
with GenericWhiteout(0.5,0.0,5.0)

window show

hi "อิวานาโกะเหรอ เห็นมีโน้ตบอกให้มารอที่นี่… ของเธอใช่มั้ย"

"ให้ตายเถอะ นั่งคิดบทดี ๆ มาทั้งบ่ายแล้วดันได้แค่นี้"

"น่าสมเพช"

"อิวานาโกะ" "อ่า… ใช่ พอดีฝากเพื่อนเอาไปให้น่ะ… ดีจังที่นายเห็น"

"รอยยิ้มที่ทั้งดีใจและเขินอายเล่นเอาเกร็งจนขยับตัวแทบไม่ได้"

stop music fadeout 10.0

window hide

scene bg op_snowywoods
show snow
with GenericWhiteout(0.5,0.0,2.0)

play sound sfx_heartslow
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.7)

with Pause(0.7)

play sound sfx_heartslow
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.7)

window show

"หัวใจเต้นแรงราวกับจะออกมาจากหน้าอกเพื่อรับเธอคนนี้ไว้เสียเอง"

window hide

scene ev other_iwanako
show snow
with GenericWhiteout(0.5,0.0,3.0)

window show

hi "คือ… เอ่อ… แล้ว ทีนี้ยังไงต่อเหรอ…"

"ลมพัดกิ่งไม้อีกครั้ง เสียงที่ดูไร้ระเบียบกลับดูน่าฟัง"

"อิวานาโกะสะดุ้งเล็กน้อยกับลมที่พัดนั้น"

"เมื่อสายลมผ่านไป เธอก็จัดแจงตัวเองราวกับมีความกล้าขึ้นมาใหม่"

"เธอจ้องมองฉัน พลางม้วนผมยาวดำขลับนั้นเล่น"

"ในใจฉันก็ตื่นเต้นตาม"

window hide

scene bg op_snowywoods
show snow
with GenericWhiteout(0.5,0.0,2.0)

window show

"รู้สึกจุกคอหอยไปหมด อยากจะพูดแต่ก็พูดไม่ออก"

"อิวานาโกะ" "ก็นะ…"

window hide

play sound sfx_heartslow
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.4)

window show

"อิวานาโกะ" "…คือว่า เรา…"

stop music fadeout 0.5

window hide

play sound sfx_heartslow
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.2)

with Pause(0.7)

play sound sfx_heartfast
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.2)

window show

"อิวานาโกะ" "… มาคบกันมั้ย…"

"ฉันยืนนิ่งอยู่อย่างนั้นเพราะใจที่เต้นอยู่"

"พอจะพูดอะไรตอบก็รู้สึกเหมือนเส้นเสียงถูกดึงจนขาด"

play music music_tragic fadein 0.05

"อิวานาโกะ" "… ฮิซาโอะ?"

"พยายามจะเปล่งเสียงออกมา แต่กลับได้ความปวดตามแขนมาแทน"

"อิวานาโกะ" "ฮิซาโอะ?!"

window hide

play sound sfx_heartfast
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.8)

window show

"ทั้งตัวขยับไม่ได้ ตาเบิกค้างด้วยความตกใจกลัว"

window hide

play sound sfx_heartfast
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.8)

with Pause(0.15)

play sound sfx_heartslow
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.8)

play sound sfx_heartfast
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.8)

with Pause(0.05)

play sound sfx_heartstop
show heartattack alpha 
with Dissolve (0.1)

stop music fadeout 0.3

show heartattack residual
with Dissolve (0.8)

window show

"อิวานาโกะ" "{b}ฮิซาโอะ{/b}!"

"จู่ ๆ หัวใจก็หยุดเต้น เข่าอ่อนแรงจนทรุดลง"

window hide

show passoutOP1
with None

nvl show Dissolve(0.2)

n "\n\n\n\n\n\n\nโลกรอบตัว—กิ่งไม้ที่ปกคลุม ฟ้าหมองมัว อิวานาโกะที่พุ่งเข้ามา—ต่างดับมืด"

n "\nสิ่งสุดท้ายที่จำได้คือเสียงของอิวานาโกะที่กรีดร้องขอความช่วยเหลือและเสียงกิ่งไม้ที่เสียดสีกันไม่หยุดหย่อน…"

nvl hide Dissolve(3.0)

nvl clear

with Pause (1.0)

scene black
with None

#**************************************
label th_NOP2:

window hide None

scene black
with None

centered "สี่เดือนมาแล้วตั้งแต่หัวใจวายครั้งนั้น" with dissolve

scene bg hosp_room
show sakura
show hospitalmask
with Dissolve (1.5)

$ renpy.music.set_volume(0.5, 0.0, channel="music")
play music music_rain fadein 4.0

nvl show dissolve

n "\n\nช่วงสี่เดือนนั้นแทบจะนับนิ้วได้เลยว่าได้ออกจากโรงพยาบาลโดยไม่มีคนตามคุมกี่ครั้ง"

n "สี่เดือนนั้นถือว่าเป็นเวลาที่นานถ้าได้อยู่แค่กับตัวเอง ถึงได้มีเวลาพอที่จะทำใจกับสถานการณ์ตัวเองได้แบบเหลือเฟือ"

n "โรคหัวใจเต้นผิดจังหวะ"

n "เป็นคำที่ฟังดูไม่น่าอภิรมย์ อยากหนีไปให้ห่าง"

n "เป็นอาการที่พบไม่มากที่ทำให้หัวใจเต้นอย่างผิดปกติหรือเร็วเกินไป และเป็นอันตรายถึงชีวิต"

n "ดูเหมือนว่าจะเป็นมานานแล้วด้วย เห็นบอกว่าปาฏิหาริย์มากที่อยู่มาได้ขนาดนี้โดยไม่เป็นอะไรเลย"

n "ปาฏิหาริย์เลยเหรอ พูดให้รู้สึกดีที่ยังรอดมาประมาณนั้นมั้ง"

n "ซึ่งไม่ได้ช่วยให้รู้สึกดีขึ้นสักเท่าไหร่"

nvl clear

n "\n\n\n\n\nส่วนพ่อแม่ก็ดูเหมือนจะช็อกกว่าฉัน ช็อกจนหัวใจจะวายแทนแล้วมั้ง"

n "\nฉันก็มีเวลาเป็นวัน ๆ ให้ทำใจหมดแล้ว แต่ทั้งพ่อแม่ก็เพิ่งรู้ ณ ตอนนั้นเลย ยอมจะขายบ้านเพื่อจ่ายค่ารักษาให้หายขาดด้วยซ้ำ"

n "\n\nซึ่งแน่นอนว่าไม่มีทางรักษาให้หายขาด"

nvl clear

n "\nเพราะเพิ่งมารู้อาการ… นี้ ถึงต้องอยู่โรงพยาบาลเพื่อพักฟื้นจากการรักษา"

n "ตอนที่มาอยู่ใหม่ ๆ ก็เหมือนมีคนคอยคิดถึงอยู่หรอก…"

n "ห้องมีแต่ดอกไม้ ลูกโป่ง การ์ด เต็มไปหมดอยู่สักสัปดาห์ได้"

n "แต่ผ่านไปไม่นานคนมาเยี่ยมก็ร่อยหรอลง ของฝากอะไรก็เริ่มหายไปจนไม่มีเลย"

n "จนมารู้ว่าที่ส่งการ์ดกับดอกไม้มาอวยพรให้เพราะแค่เป็นงานห้องเฉย ๆ"

n "คนเป็นห่วงจริง ๆ ก็คงมีแหละ แต่ก็นะ ขนาดทีแรกยังแทบไม่มีคนมาเลย ผ่านไปเดือนนึงก็มีแค่พ่อแม่ที่มาเป็นประจำ"

n "อิวานาโกะเป็นคนสุดท้ายที่หายหน้าไป"

n "ผ่านไปหกสัปดาห์ก็ไม่เห็นเธออีกเลย แต่ยังไงตอนมาเยี่ยมก็แทบไม่ได้คุยกันอยู่แล้ว"

n "เราไม่ได้คุยเรื่องวันหิมะตกนั้นอีกเลย"

nvl clear

n "\nโรงพยาบาลเหรอ"

n "ไม่ใช่ที่ที่อยากอยู่นักหรอก"

n "ทั้งหมอทั้งพยาบาลก็รู้สึกเหมือนเป็นคนแปลกหน้ายังไงไม่รู้"

n "คงเพราะรีบแล้วก็มีคนไข้อีกเป็นล้านรออยู่ละมั้ง แต่ก็รู้สึกอึดอัดเหมือนกัน"

n "ช่วงเดือนแรก ๆ พอเห็นหมอหัวใจก็คอยถามอยู่ว่าจะออกได้สักเมื่อไหร่"

n "เขาไม่เคยตอบอะไรตรง ๆ เลย บอกแค่ว่ารอดูว่าการรักษากับการผ่าตัดจะได้ผลมั้ย"

n "\nเลยมานั่งมองแผลเป็นหลังผ่าตัดที่ค่อย ๆ เปลี่ยนรูปไปเรื่อย ๆ มองเหมือนเป็นตัวกาลกิณีสักอย่าง"

n "ก็ยังถามเขาเรื่องออกจากโรงพยาลอยู่นั่นแหละ ถึงจะไม่ได้คาดหวังอะไรจากคำตอบที่ไม่เคยมีจากรอบก่อน ๆ แล้ว\nแต่ถ้ายังเลี่ยง ๆ ไปมาแบบนี้ก็อาจจะพอมีหวังอยู่"
    
nvl clear

n "\n\n\n\nพอถึงจุดนึงก็ไม่ได้ดูทีวีเลย ไม่รู้หรอกทำไม แต่ก็ไม่ได้ดูแล้ว"

n "อาจจะเพราะดูไปก็ไม่ได้รู้สึกเป็นอิสระจากสถานการณ์ตอนนี้เท่าไหร่"

n "\nเลยหันมาอ่านหนังสือแทน ที่โรงพยาบาลมี “ห้องสมุด” อยู่ ถึงจะเหมือนเป็นแค่ห้องเก็บหนังสือเฉย ๆ ก็เถอะ ก็ค่อย ๆ อ่านไปทีละกอง พออ่านกองนึงหมดแล้วก็ไปเอามาใหม่"

n "ถึงได้รู้ตัวว่าชอบอ่านหนังสือ ออกจะเสพติดด้วยซ้ำ เริ่มรู้สึกเหมือนพอไม่มีหนังสือติดมือแล้วเหมือนขาดอะไรไป"

n "\nฉันชอบอ่านเรื่องราว"

nvl clear

n "\nนั่นแหละชีวิตฉัน"

n "\nวัน ๆ เริ่มเหมือนกันไปหมด ต่างก็แค่หนังสือที่อ่านกับสภาพอากาศข้างนอก เหมือนเวลาเป็นก้อนอะไรใหญ่ ๆ สักอย่างที่ฉันติดอยู่ข้างใน ไม่ได้เคลื่อนไปไหน"

n "บางทีสัปดาห์นึงผ่านไปยังไม่รู้ตัวด้วยซ้ำ"

n "บางทีก็มานั่งคิดว่าวันนี้วันอะไร"

n "แต่ก็มีบางทีที่ทุกอย่างเข้ากระทบประสาทรับรู้ของฉันอย่างหนักหน่วงผ่านกำแพงความเมินเฉยที่ฉันสร้างขึ้นมา" 

n "รู้สึกเหมือนจับหนังสือก็เจ็บ ในอกก็แน่นไปหมดจนต้องวางหนังสือแล้วนอนมองเพดานอยู่เฉย ๆ สักพักเหมือนจะร้องไห้" 

n "แต่ก็เป็นแบบนั้นไม่บ่อย"

n "\nแถมร้องไห้ไม่ออกด้วยซ้ำ"

$ renpy.music.set_volume(1.0, 1.0, channel="music")

nvl hide dissolve

nvl clear

window show

"วันนี้หมอเข้ามายิ้มทักทาย เหมือนจะดูตื่นเต้นแต่ก็ไม่ ออกแนวพยายามร่าเริงแทนฉันมากกว่า"

"พ่อแม่ก็มาด้วย ไม่เห็นหน้ามาสองสามวันแล้วเหมือนกัน ทั้งคู่ก็ดูจะแต่งตัวมาดีด้วย แต่เนื่องในโอกาสอะไร\nไม่ได้จะจัดงานเลี้ยงนี่"

"หมอหัวใจจะชอบทำแบบนี้ประจำ จะอยู่สักพักก่อน แล้วก็จัดเอกสาร\nแล้วก็วางไว้เหมือนจะให้ที่ทำอะไรไร้สาระเมื่อกี้มีสาระขึ้นมา"

"แล้วก็มานั่งสบาย ๆ ตรงริมเตียงที่อยู่ถัดกัน มองตาอยู่สักพักหนึ่ง"

"หมอ" "สวัสดีฮิซาโอะ วันนี้เป็นไงบ้าง"
   
"ฉันไม่ตอบ แต่ยิ้มน้อย ๆ กลับไปให้"

"หมอ" "เธอน่าจะกลับบ้านได้แล้วนะ หัวใจก็แข็งแรงขึ้นแล้ว ถ้าระวังตัวอะไรอีกหน่อยก็ใช้ได้แล้วละ"

"หมอ" "หมอสั่งยาไว้ให้แล้ว เดี๋ยวจะให้ใบสั่งยากับพ่อเธอไว้นะ"

"หมอให้กระดาษใบหนึ่งกับพ่อ ซึ่งสีหน้าดูเครียดขึ้นมาทันทีที่กวาดตาอ่าน"

"พ่อ" "เยอะมาก…" 

"ฉันหยิบมาจากพ่อแล้วอ่านบ้าง ทั้งตัวชาวาบ จะให้ว่ายังไงล่ะเนี่ย"

$ renpy.music.set_volume(0.5, 2.0, channel="music")

scene white
with Dissolve(2.0)

show wallodrugs:
    xalign 0.0 subpixel True
    easein 25.0 xalign 1.0


"รายการยายาวเหยียดที่จ้องมองฉันมาจากกระดาษดูจะมากมายเกินไป ปนระคนกันอยู่ในทะเลตัวหนังสือ"

"บ้าไปแล้ว"

"ผลข้างเคียง อาการไม่พึงประสงค์ ข้อห้ามใช้ ขนาดยา ทั้งหมดเรียงบรรทัดกันเน้นย้ำอย่างหนักแน่น"

"พยายามอ่านแล้วแต่ก็ไร้ผล"

"ไม่เข้าใจอะไรสักอย่างเลย ยิ่งคิดก็ยิ่งเวียนหัว"

"ทั้งหมดนี่… ทุกวัน ไปตลอดชีวิต น่ะนะ"

$ renpy.music.set_volume(1.0, 1.0, channel="music")

scene bg hosp_room
show sakura
show hospitalmask
with fade

"หมอ" "หมอเกรงว่านี่คือสิ่งที่ดีที่สุดที่หมอจะทำได้แล้ว"

"หมอ" "แต่ว่ายาใหม่ ๆ ก็ถูกพัฒนาเรื่อย ๆ อยู่แล้ว ถ้านาน ๆ ไปบางตัวจะหายไปบ้างก็คงไม่แปลกใจ"

"นาน ๆ ไป… อันนี้คือจะให้ใจชื้นขึ้นหรืออะไร ถ้าไม่พูดอะไรเลยยังจะดีกว่ามั้ง…"

"หมอ" "แล้วก็ หมอคุยกับพ่อแม่เธอแล้วนะว่าไม่ต้องกลับไปเรียนที่เดิมแล้วจะดีกว่า"

"อะไรนะ!"

"พ่อ" "ใจเย็นก่อนนะฮิซาโอะ ฟังหมอเขาพูดก่อน…"

"ใจเย็นเหรอ ดูที่พูดนี่ก็คงรู้แล้วมั้งว่าจะไม่เอาด้วย นี่จะได้โฮมสคูลเหรอ"

"แต่ความไม่สบายใจใด ๆ ที่แสดงออกมาก็ถูกเมิน"

"หมอ" "หมอรู้ว่าการศึกษาของเธอนั้นสำคัญ แต่ว่าถ้าไปโดยไม่มีคนคอยดูแลคงไม่ดีเท่าไหร่"

"หมอ" "อย่างน้อย ๆ ก็จนกว่าจะแน่ใจว่ายาที่สั่งไปลงตัวแล้ว"

"หมอ" "ก็เลยคุยกับพ่อแม่เธอเรื่องย้ายโรงเรียน"

"หมอ" "จะได้ย้ายไปที่โรงเรียนยามากุที่จัดตั้งมาสำหรับนักเรียนพิการ"

"พิการเหรอ อะไรนะ นี่ฉัน…"

"หมอ" "มีทีมพยาบาลอยู่ตลอดยี่สิบสี่ชั่วโมง แถมเดินทางไม่กี่นาทีก็ถึงโรงพยาบาลใหญ่ชั้นนำด้วย\nนักเรียนส่วนใหญ่ก็อยู่หอในนั่นแหละ"

"หมอ" "ก็ไม่ต่างอะไรกับโรงเรียนกินนอนหรอก ออกแบบมาให้นักเรียนมีอิสระพอตัวไปพร้อม ๆ กับที่มีคนคอยดูอยู่ตลอดด้วย" #possibly longest text for Thai before slash n

"อิสระเหรอ ยังไงมันก็โรงเรียนสำหรับคนพิการนั่นแหละ อย่าเลี่ยงไปมาสิ"

"ถ้า “อิสระ” ขนาดนั้นจริง ๆ จุดขายก็คงไม่ใช่ทีมพยาบาลยี่สิบสี่ชั่วโมงกับความใกล้โรงพยาบาลหรอก"

"พ่อ" "ซึ่งก็ต้องถามความเห็นลูกก่อนอยู่ดีน่ะนะ แต่ว่า… แม่กับพ่อคงอยู่โฮมสคูลให้ไม่ได้"

"พ่อ" "แม่กับพ่อก็ไปดูมาเมื่อสองสามอาทิตย์ก่อนแล้ว ลูกน่าจะชอบนะ"

"โอเค คงไม่มีทางเลือกจริง ๆ นั่นละ"

"หมอ" "ถ้าเทียบกับโรคหัวใจอย่างอื่น คนที่มีอาการอย่างเธอปกติก็อยู่ได้นานเลย\nเป็นโอกาสอันดีที่จะได้กลับมาเรียนด้วย สักวันเธอก็ต้องหางานทำอีก"

"ไม่ใช่โอกาสสักหน่อย อย่าเรียกว่าโอกาสเลย โอกงโอกาสอะไร"

"หมอ" "ได้กลับเข้าโรงเรียนแบบนี้ต้องตื่นเต้นสิ เห็นเธออยากกลับไปเรียนนี่ ถึงจะไม่ใช่ที่เดิมก็เถอะ…"

"โรงเรียนพิเศษเหรอ นี่จะ…"

"ตบหน้ากันหรืออะไร อยากบอกแบบนั้นแหละ เหมือนซ้ำเติมกันอีก"

"พ่อ" "ไม่ใช่อย่างที่ลูกคิดหรอก นักเรียนที่นั่นเขาต่างก็กระตือรือร้นกันในแบบของตัวเองดี"

"พ่อ" "เป็นโรงเรียนเพื่อคนที่ยังพอใช้ชีวิตเรียนรู้อะไรได้ที่อาจจะต้องการความช่วยเหลือ… สักหน่อย"

"หมอ" "พ่อเธอพูดถูก ศิษย์เก่าที่นั่นหลายคนจบไปก็ทำอะไรหลายอย่าง คนเราเอาความพิการมารั้งไว้ไม่ได้หรอก"

"หมอ" "เพื่อนร่วมงานหมอคนนึงที่อยู่อีกโรงพยาบาลก็จบจากที่นั่น"

"สนที่ไหนล่ะ คนเราเอาความพิการมารั้งไว้ไม่ได้เหรอ ก็ความพิการนั่นแหละที่จะรั้งไว้ไม่ใช่เหรอ"

"ไม่ชอบเลยที่เรื่องใหญ่แบบนี้แต่มีคนมาคิดแทนให้ แต่ทำไงได้ ชีวิต “ปกติ” คงไม่มีอีกแล้วละ"

stop music fadeout 10.0

"ตลกดี เคยคิดอยู่ว่าชีวิตที่ผ่านมาดูน่าเบื่อ แต่ทีนี้กลับอยากเป็นเหมือนเดิม"

"อยากจะประท้วง อยากจะโทษตัวเองที่ไม่มีอาการช็อกหรือเป็นลมอะไรเลย จะตะโกนอะไรก็ได้\nตะโกนว่ายังกลับไปเรียนที่เดิมได้ แต่ไม่มีเลย"

"ไม่ได้พูดอะไรเลย เพราะรู้ว่าทำไปก็เปล่าประโยชน์"

"ฉันมองไปรอบ ๆ ห้องด้วยความรู้สึกหมดอาลัยตายอยากกับทุกสิ่ง ทั้งโรงพยาบาล ทั้งหมอ ทั้งโรคนี้ ทุกอย่าง\nไม่มีอะไรที่จะทำให้รู้สึกต่างออกไปเลย"

"แม้รู้ดีว่าไม่มีทางเลือกแล้ว แต่พอคิดว่าต้องไปโรงเรียนคนพิการ… จะเป็นยังไงกัน\nพยายามจะนึกภาพในแง่บวกก็นึกแทบไม่ออก"

"แต่ขอลองดูสักตั้ง"

"เริ่มใหม่ทั้งหมดก็ไม่แย่"

"พอคิดอย่างนั้นแล้วก็พอจะมีแรงไปต่อ อย่างน้อยก็ยังมีบางอย่างให้มองอยู่ ถึงจะเป็น “โรงเรียนพิเศษ”\nแต่ก็เป็นการเริ่มต้นใหม่ทั้งหมด ชีวิตฉันก็ยังไม่จบสิ้นสักหน่อย ถ้าเอาแต่คิดลบแล้วยอมแพ้ไปก็คงใช่ที่"

"อย่างน้อย ๆ ก็ลองดูก่อนว่าชีวิตใหม่ของฉันจะเป็นยังไง"

window hide



#**************************************
label th_A1:

window hide None

scene bg school_gate
with Dissolve(3.0)

play music music_happiness

window show

"ประตูดูจะหรูหราเกินหน้าที่ไปมาก"

"อันที่จริง ประตูส่วนใหญ่ก็เป็นอย่างนั้นอยู่แล้ว แต่บานนี้ดูจะหรูเป็นพิเศษ"

"อิฐสีแดง เหล็กดัดประณีตกับปูนสีเทา ดู ๆ แล้วไม่น่าเข้าเลย"

"นึกสงสัยอยู่เหมือนกันว่าประตูโรงเรียนต้องเป็นแบบนี้หรือเปล่า แต่ก็ไม่รู้เหมือนกัน คงไม่มั้ง"

"แต่ก็ไม่อยากมาพินิจประตูให้เสียเวลานานจึงเดินเร็วเข้าไป ซึ่งทำให้รู้สึกดีอย่างเหลือเชื่อ"

"การก้าวไปข้างหน้านั้นทำให้รู้สึกดี"

scene bg school_courtyard
with locationchange

"แล้วเดินมายังอาคารหลักของโรงเรียนยามากุด้วยการเดินเร็วนั้น ตอนนี้อยู่ตัวคนเดียว\nเพราะพ่อแม่กำลังย้ายของเข้าหอให้ แถมจะมีคนรอฉันอยู่ด้วย"

"สนามโดยรอบมีหญ้าโตเขียวขจี"

"ซึ่งดูไม่เหมือนสนามโรงเรียนทั่ว ๆ ไป เหมือนสวนสาธารณะมากกว่า ทั้งทางเดินที่ตัดผ่านตามต้นไม้ \nกลิ่นหญ้าที่ตัดใหม่ ๆ แล้วก็อะไร ๆ ที่ทำให้นึกถึงสวนสาธารณะ"

"คำจำพวก “สะอาด” และ “ปลอดเชื้อ” แวบเข้ามาจนเสียวสันหลังวาบ"

"ฉันพยายามสลัดคำเหล่านั้นออก ต้องพยายามเปิดใจไว้ นี่คือชีวิตใหม่ของฉัน ไม่ว่ายังไงก็ต้องไปต่อ"

"คอยบอกตัวเองอย่างนั้น"

"ตึกใหญ่สองสามตึกตั้งอยู่เบื้องหลังกลุ่มใบไม้เหล่านั้น ที่ดูใหญ่และมากเกินกว่าจะเป็นโรงเรียน"

"ทุกอย่างดูแปลก ๆ ต่างไปจากที่คิดเอาไว้"

"เป็นภาพที่ดูคุ้นเคย แต่ดันทำให้รู้สึกประหลาด ทั้งที่รู้แล้วว่าที่นี่จะเป็นโรงเรียนใหม่ของฉัน\nแต่ในหัวเหมือนจะบอกว่าไม่ใช่"

"ไม่รู้ว่าความรู้สึกนี้เป็นเพราะภาพจำกับโรงเรียนสำหรับคนพิการหรือเปล่า"

"แต่พูดถึงคน ก็ไม่เห็นมีสักคน แอบน่ากลัวเหมือนกัน"

"อยากจะให้มีสักคนเอาไว้อยู่เป็นตัวเป็นตนแทนความรู้สึกเหมือนอยู่คนละมิติแบบที่เป็นตอนนี้"

"เสียงใบไม้ไหวกับลมและหญ้าเขียวขจีดึงความสนใจฉัน"

"เห็นแล้วก็นึกถึงโรงพยาบาลอีก ยิ่งชอบบอกว่าที่ทาสีเขียวเพราะเป็นสีที่สงบดี"

"แต่ทำไมยังรู้สึกไม่สงบ ทั้งที่สีเขียวเยอะขนาดนี้"

"…"

"ต่อเมื่อมายืนหน้าอาคารหลักที่ตั้งตระหง่าน จึงนึกขึ้นได้ว่าทำไมอึกอักอยู่ที่ประตูเสียนาน"

"เพราะนั่นคือโอกาสสุดท้ายที่ฉันจะถอนตัว ถึงจะถอนตัวไปแล้วก็ไม่มีชีวิตให้กลับไปใช้อีกก็เถอะ"

"แต่ก็ ถ้าเข้ามาเมื่อไหร่ก็ไม่มีทางกลับไปได้แล้ว"

"ฉันเปิดประตูเข้าไปอย่างประหม่าพร้อมความคิดในหัวเมื่อครู่นั้น"

scene bg school_lobby
with locationchange

"ชายตัวสูงที่ท่ายืนแปลก ๆ คนหนึ่งหันมามองฉัน ซึ่งก็ไม่แปลกเพราะทั้งโถงมีแค่เราสองคน"

show muto normal at center
with charaenter

mu_ "เธอคือ… นิ… นา… นิกิใช่มั้ย"

hi "นากาอิครับ"

show muto smile
with charachange

mu_ "เธอน่ะเอง เยี่ยม ฉันคือครูประจำชั้นเธอนะ สอนวิทยาศาสตร์ ชื่อมุโต้"

mu "ยินดีต้อนรับ"

"พวกเราจับมือกันอย่างพอเหมาะพอดีแล้วเขาก็มองที่นาฬิกาข้อมือ"

show muto irritated
with charachange

mu "เห็นหัวหน้าทีมพยาบาลบอกให้เธอแวะไปตรวจร่างกายครั้งแรกหน่อยด้วย แต่ไว้ทีหลังแล้วกัน"

hi "ได้เหรอครับ"

show muto normal
with charachange

mu "ได้สิ ช่วงบ่าย ๆ น่าจะได้ ตอนนี้ต้องพาเธอไปแนะนำตัวกับชั้นเรียนก่อน น่าจะรอกันอยู่"

"รออยู่เหรอ ไม่ค่อยชอบเป็นจุดศูนย์กลางความสนใจเท่าไหร่ แต่สถานการณ์อย่างนี้ก็คงช่วยไม่ได้ละนะ"

"แต่พอไม่รู้ว่ามีอะไรรออยู่แล้วก็รู้สึกประหม่ายังไงไม่รู้"

"คิดไปคิดมาก็เกือบลืมฟังว่าครูพูดอะไรอยู่"

label th_choiceA1:
menu:
    with menueffect

#choice:
    mu "จะแนะนำตัวหน้าห้องมั้ย"

    "ทำไมล่ะครับ":
        return m1
        
    "ได้ครับ":
        return m2

label th_A1a:

hi "ทำไมล่ะครับ จำเป็นต้องทำเหรอครับ"

mu "ไม่จำเป็นหรอก ถึงได้ถามก่อนนี่แหละ"

hi "โอเคครับ"

mu "งั้นก็ไปกัน"



label th_A1b:

hi "ได้ครับ แต่ไม่ใช่ว่าต้องแนะนำตัวอยู่แล้วเหรอครับ"

mu "ก็ใช่ แต่บางคนก็ไม่อยากเป็นจุดศูนย์กลางความสนใจหรอก"

"ฉันก็คงเป็นคนพวกนั้นด้วย แต่อย่างน้อยเห็นหน้าค่าตากันครั้งแรกก็คงต้องทำให้ดีหน่อย"

hi "นั่นสิครับ แต่ผมได้ครับ"

mu "งั้นก็ไปกัน"



label th_A1c:

scene bg school_staircase2
with locationchange

"ใจที่เต้นอยู่ในอกระหว่างที่เดินขึ้นบันไดตามครูไปคอยเตือนถึงอาการที่เป็นอยู่"

scene bg school_hallway3
show muto normal at center
with locationchange

"ห้องที่สามบนชั้นสามมีป้ายบอกว่าสำหรับห้อง 3-3"

play sound sfx_dooropen

"มุโต้เปิดประตูเข้าห้องไป"

show muto normal at Alphaout(0.5), Slide(0.5,0.5,0.4,0.5,0.5,_ease_out_time_warp)
with Pause (0.5)

hide muto
with None

mu "อรุณสวัสดิ์ทุกคน ขอโทษที่มาช้าอีกแล้วนะ"

stop music fadeout 2.0

"แวบหนึ่งนึกลังเลยืนชะงักอยู่ที่ประตู"



#**************************************
   
label th_A2:

scene bg school_hallway3
with None

"อา ตั้งสติหน่อย! นี่คือก้าวสำคัญ ยังไง… คิดมากไปตอนนี้ก็คงไม่ได้อะไรขึ้นมา"

scene ev hisao_class_start
with fade

play music music_normal fadein 0.5

"ระหว่างที่เดินตามครูเข้าห้องก็มองไปรอบ ๆ ส่วนหนึ่งก็เพราะจะได้ไม่ต้องสบสายตาที่ต่างอยากรู้ของคนในห้อง"

"เป็นห้องที่ค่อนข้างกว้าง เพดานสูงมาก ๆ โต๊ะก็ตั้งห่างกัน"

"กระดานกินที่ทั้งผนังด้านหนึ่ง หน้าต่างทรงสูงก็ทำให้ห้องดูกว้างไปอีก"

"โต๊ะนักเรียนก็เป็นโต๊ะธรรมดาที่มีที่เก็บของอยู่ข้างใต้ เก้าอี้ก็ทำจากไม้กับโครงเหล็ก เรียบง่ายและใช้งานดี"

"ฉันมายืนอยู่ที่หน้าห้องแล้วมองคนอื่น ๆ ทุกคนก็ดูปกติเหมือนนักเรียนทั่ว ๆ ไป\nแต่ถ้างั้นจริงก็คงไม่มาอยู่ที่นี่หรอกแหละนะ"

scene ev hisao_class_move
with None

"ก็คงเหมือนฉันที่มีอาการผิดปกติสักอย่าง แค่ว่าอาจจะไม่ได้เห็นชัดขนาดนั้น\nทว่าก็ดันไปเห็นผู้หญิงคนหนึ่งที่นิ้วโป้งข้างขวาหายไป ดูแล้วก็หลอนหน่อย ๆ"

"ถึงปกติคนจะฟังเวลามีคนพูดถึงตัวเอง แต่ก็แทบไม่ได้ฟังตอนที่ครูแนะนำตัวฉันกับคนในห้อง"

"ฉันเหลือบไปเห็นคนหนึ่งที่มีผมสีดำที่มองมาทางนี้ ผู้หญิงผมตรงยาวที่ค่อนข้างสะดุดตา\nพอเห็นว่าฉันมองอยู่ก็เอามือปิดหน้าเหมือนกับว่าทำแล้วจะล่องหนได้"

"แล้วก็มีผู้ชายคนหนึ่งที่มีไม้เท้าพิงอยู่กับตู้ล็อกเกอร์ในห้อง แปลกดีเหมือนกันที่เห็นคนใช้ไม้เท้าแต่เด็ก"

"แล้วก็ผู้หญิงอีกคนที่ขยับมือไปมาแปลก ๆ อยู่ ภาษามือมั้ง เธอเหลือบมองลอดแว่นมาก่อนจะกลับไปทำอะไรต่อ"

"ดูน่ารักดี ผู้หญิงผมสีชมพูที่ดูร่าเริงที่อยู่ข้าง ๆ ก็เหมือนกัน เตะตามาแต่ไกล\nยังงงอยู่เหมือนกันว่าทำไมตอนเดินเข้ามาถึงไม่เห็น…"

mu "…ต้อนรับเพื่อนใหม่ด้วย"

"พอเขาตบมือทุกคนก็ตบตาม ยกเว้นผู้หญิงคนหนึ่งที่แถวหน้าที่มีมือเดียว\nฉันตกใจนิดหน่อยแต่ก็ทำเป็นโค้งตัวสำหรับเสียงปรบมือที่เกินจะรับไว้นี้"


label th_A2a:

"พอเสียงปรบมือสิ้นความเงียบที่ไม่มีใครอยากทำลายก็เข้าปกคลุม"

"ครูก็นึกได้ว่าต้องพูดอะไรต่อ อ้าปากทำเสียงงึมงำอยู่แล้วก็เงียบไป แล้วก็แนะนำตัวฉัน"

"ดูไม่มีใครสนใจเท่าไหร่"

"รู้งี้ยอมแนะนำตัวเองไปก็ดี"

"คงเพราะเพิ่งนึกได้ว่ายังไม่ได้รู้จักฉันเลย ครูก็ได้แต่พูดชื่อฉัน ซึ่งก็พูดผิด แล้วให้ฉันเขียนชื่อบนกระดาน"

"ฉันก็เขียนแล้วหันหน้ากลับไปพร้อมความรู้สึกกระอักกระอ่วน"


label th_A2b:

"ทุกคนที่พร้อมใจกันเงียบเป็นสัญญาณว่าฉันต้องเปิดปากได้แล้ว"

hi "เอ่อ… ผมฮิซาโอะ นากาอิ"

"แล้วไงอีก"

hi "ชอบอ่านหนังสือกับเตะบอล ถึงจะเพิ่งมาใหม่ แต่ก็ฝากเนื้อฝากตัวด้วยนะครับ"

"แล้วไงอีก"

"ทำตัวน่าเบื่อชะมัด ใคร ๆ เขาก็แนะนำตัวกันแบบนี้ทั้งนั้น ต้องพูดอะไรที่ตื่นเต้นกว่านี้หน่อยสิ"

"สุดท้ายก็ไม่ได้พูดอะไร ส่วนครูก็รับไม้ต่อไป"

"แต่ทุกคนก็ดูจะพอใจถึงแทบจะไม่ได้พูดอะไร ผู้หญิงสองสามคนกระซิบกระซาบกัน ก็ยังดีละมั้งที่ออกมาเป็นแบบนี้" 

"…"


label th_A2c:

scene ev hisao_class_end
with None

"ฉันคอยฟังครูที่พูดเรื่องเพื่อนใหม่ สายตาก็มองไปรอบ ๆ ห้อง"

"ทุกคนดูจะตั้งใจฟัง และพอครูพูดจบทุกคนก็ปรบมืออีกครั้ง ซึ่งก็ดูแปลก ๆ"

"คราวนี้ผู้หญิงที่อยู่แถวหน้าปรบมือแล้ว แต่เสียงก็ตัน ๆ เพราะเป็นแค่มือเดียวตบอยู่กับข้อมืออีกข้างที่มีผ้าพันอยู่" 

"เห็นแล้วก็แอบรู้สึกผิดเหมือนกัน"

scene bg school_scienceroom at bgright
show muto normal at center
with fade

mu "วันนี้จะทำงานกลุ่มนะ จะได้มีโอกาสคุยกับคนอื่นด้วย โอเคมั้ย"

hi "ครับ ได้ครับ"

show muto smile
with charachange

mu "ดี งั้นเธออยู่กับฮากามิจิแล้วกันนะ เธอเป็นหัวหน้าห้องน่ะ"

mu "เผื่อมีอะไรสงสัยจะได้ตอบให้ได้ คงไม่มีใครเหมาะกว่านี้แล้วนี่นะ"

hide muto
with charaexit

"จะไปรู้เหรอ"

"ครูก็แจกงานให้แล้วบอกว่าให้จับกลุ่มกันสามคน"

"ถึงพึ่งรู้ตัวว่ายังไม่รู้เลยว่าฮากามิจิคือคนไหน ความรู้สึกช้าเกิน ครูก็เหมือนจะเห็นว่ากำลังงงอยู่"

mu "อ้อ จริงสิ ฮากามิจิคือคนนั้นนะ ชิซูเนะ ฮากามิจิ"

show misha perky_smile at center
with charaenter

"พอถูกเรียกชื่อ สาวผมสีชมพูที่ดูร่าเริงตาสีทองคนนั้นก็โบกมือให้ ฉันเข้าไปนั่งข้างเธอตรงข้างหน้าต่าง"

hi "ไง เธอฮากามิจิใช่มั้ย ยินดีที่ได้รู้จัก"

stop music fadeout 1.0

show misha cross_laugh
with charachange

mi_shi "ฮ่าฮ่าฮ่า~!"

"หา? อยู่ ๆ ก็หัวเราะไม่ทันตั้งตัว"

show misha hips_grin
with charachange

mi_shi "ยินดีที่ได้รู้จักเช่นกัน!{w=0.5} แต่~!"

mi_not_shi "ยินดีที่ได้รู้จักเช่นกัน! แต่~!{fast} ฉันไม่ใช่ฮากามิจิ ฉันมิช่า คนนี้ต่างหากฮากามิจิ ชิจัง~!"

play music music_shizune fadein 1.0

show misha hips_grin at twoleft
show bg school_scienceroom at center
with charamove

show shizu basic_normal at tworight
with charaenter

"มิช่าชี้ไปที่ผู้หญิงข้าง ๆ เธอพลางหัวเราะคิกคัก เป็นคนที่เห็นใช้ภาษามือเมื่อกี้ เหมือนจะจ้องฉันมาตลอดเลย\nเธอพยักหน้าดูไม่ยี่หระอะไร เพียงรับรู้ว่ามีฉันอยู่ด้วย... แต่ก็แค่นั้น"

"ผมสั้นที่หวีเรียบแปล้ แว่นทรงรีที่วางอยู่บนจมูกบาง กับนัยน์ตาสีน้ำเงินที่ดูทั้งสนใจกับเบื่อเล็กน้อยสลับไปมาตลอด"

hi "ยินดีที่ได้รู้จัก"

show shizu behind_blank 
with charachange

shi "…"

show misha perky_smile 
with charachange

show misha sign_smile 
with charachange

show misha perky_smile 
with charachange

"เธอมองขวับไปทางมิช่าที่ยิ้มและทำมือไปมา"

show shizu adjust_happy 
with charachange

show shizu behind_smile 
with charachange

"ฮากามิจิพยักหน้าและทำมือไปมาบ้าง"

"ชักสงสัยว่าครูอำกันเล่นหรือเปล่า ทั้งที่บอกว่า “มีโอกาสคุยกับคนอื่นด้วย” กับ “มีอะไรสงสัยจะได้ตอบให้ได้”"

show misha hips_smile 
with charachange

mi "ยังงง ๆ อยู่ใช่มั้ย ๆ แต่ก็ไม่แปลกหรอกที่จะนึกว่าฉันคือชิจัง!"

mi "ชิจังเขาหูหนวกน่ะ ฉันก็เลยคอยเป็นคนแปลคำพูดให้"

show misha hips_grin 
with charachange

mi "เหมือนล่ามน่ะแหละ~! เมื่อกี้บอกว่า ยินดีที่ได้รู้จักเช่นกันนะ!"

show shizu basic_normal2 
with charachange

shi "…"

show misha perky_smile 
with charachange

mi "นายเป็นเด็กใหม่ใช่มั้ย เอ่อ ชิจัง ก็ต้องใช่สิ! ถ้าไม่ใช่ก็คงไม่ไปยืนอยู่หน้าห้องเฉย ๆ หรอก ใช่มั้ย ๆ ~!"



label th_A2d:

mi "ดูเป็นคนที่น่าสนใจดีใช่มั้ยล่ะ~!"



label th_A2e:

"มิช่ามองมาทางฉันด้วยสีหน้าแปลก ๆ แล้วพูดต่อ"

mi "ตอนนี้ยังไม่รู้จักมาก แต่เดี๋ยวก็คงรู้แหละ"

"รู้งี้แนะนำตัวเสียก็ดี อย่างน้อยได้พูดอะไรก็คงดีกว่าปล่อยให้ครูพูดเอื่อย ๆ กับพูดชื่อผิดแบบนั้น"



label th_A2f:

mi "รู้มาว่าจะมีนักเรียนใหม่มา แต่ไม่นึกว่าจะมาวันนี้ เร็วจัง! ฮิจังใช่มั้ย"

"ฮิจัง… เหรอ"

show misha hips_grin 
with charachange

mi "ช่าย~! เหมาะดีออก"

"เมื่อกี้ได้พูดด้วยเหรอ ตกใจหมด ไม่ชอบชื่อนั้นเลยแฮะ"

hi "เหมาะตรงไหน"

show misha cross_grin 
with charachange

mi "เหมาะสิ~! หน้าตาก็เหมือนที่คิดไว้เปี๊ยบเลย!"

show shizu adjust_smug 
with charachange

shi "…"

show misha cross_laugh 
show shizu adjust_happy 
with charachange

mi "ฮ่าฮ่าฮ่าฮ่า~! ใช่ นายนี่แหละคือฮิจังเลย!"

hi "ทำไมถึงคิดงั้นกัน…"

shi "…"    

"ฮากามิจิใช้นิ้วเคาะที่โต๊ะเพื่อให้มิช่าหัน ทั้งสองคนทำท่าไปมาอย่างตื่นเต้นจนดูแทบไม่ทัน"

show shizu adjust_happy 
with charachange

show misha sign_smile 
with charachange

show shizu behind_smile 
with charachange

show misha perky_confused 
with charachange

"มิช่าดูจะรับข้อมูลมากไม่ไหว"

show misha hips_grin 
with charachange

mi "อะฮ่าฮ่า~! เอ่อ ขอโทษทีนะ!"

show misha hips_smile 
with charachange

mi "ชิจังจะบอกว่าตัวเองน่ะเป็นหัวหน้าห้องนะ ถ้าอยากรู้อะไรก็ถามได้ไม่ต้องเกรงใจ"

show shizu behind_blank 
with charachange

shi "…"

show misha sign_smile
with charachange

mi "มาวันแรกเป็นไงบ้าง ถ้าไม่มีเวลาเดินดูให้ทั่วก็ให้พวกเราพาไปเดินให้นาย…{w=0.5}{nw}"

show misha perky_smile:
    "misha perky_confused" with Dissolve(0.5, alpha=True)
with None

extend "คุ้นชิน?{w=0.5}{nw}"

show misha perky_confused:
    "misha perky_smile" with Dissolve(0.5, alpha=True)
with None

extend "กับที่นี่ได้นะ!"

"มิช่าชะงักไปนิดหน่อยกับคำยากนั้น ทำให้ที่แปลมาลื่น ๆ ปกติดูสะดุด"

hi "ขอบใจ ช่วยได้เยอะเลย อื้อ วันนี้มาถึงก็มาเรียนเลย"

show shizu behind_frown 
with charachange

shi "…"

show misha hips_laugh 
with charachange

mi "ฮ่าฮ่าฮ่า~!"

show misha hips_smile 
with charachange

mi "ไม่ได้นะ! ปกติจะไปไหนต้องหาข้อมูลที่นั่นมาก่อนสิ แล้วก็ไม่ใช่แค่โรงเรียนด้วย~!"

show misha hips_grin 
with charachange

mi "ต้องรู้มาก่อน! ต่อให้จะไปแค่ร้านสะดวกซื้อก็เถอะ! เอาจริงปะเนี่ยชิจัง ฮ่าฮ่าฮ่า~!"

show misha perky_smile 
show shizu behind_smile 
with charachange

"รู้มาก่อนเหรอ ฉันคงไม่ได้มีอารมณ์อยากจะทำขนาดนั้นละมั้ง"

"ไม่ได้อยากมาเลย ถึงจะลากให้ร่างมาอยู่ได้แล้วก็เถอะ แต่ก็นะ"

"ฉันไม่ได้พูดอะไร ส่วนมิช่าก็ทำภาษามือแล้วยักไหล่ตาม คุยอะไรกัน เรื่องฉันมั้ง"

"อยากจะไหลลงไปจากเก้าอี้ ทั้งสองคนยิ้ม แต่พอฉันเห็นท่ายักไหล่นั้นแล้วก็รู้สึกบางอย่าง"

show misha perky_sad 
with charachange

mi "ดูไม่ร่าเริงเลยนะ ไหวมั้ยเนี่ย"

show shizu basic_normal 
with charachange

shi "…"

show misha perky_smile 
with charachange

mi "อย่าเข้าใจผิดสิ~! พอเห็นคนไม่กล้าถามแล้วไม่ชอบเลย! คนเราต้องถามสิถึงจะรู้~!"

mi "จะขอความช่วยเหลือหรือถามอะไรน่ะไม่แปลกเลย! เลิกทำหน้าเหมือนสอบตกมาได้แล้วน่า!"

show misha cross_laugh 
with charachange

mi "วะฮ่าฮ่าฮ่า~!"

hi "โอเค"

show shizu adjust_happy 
with charachange

shi "…"

show misha perky_smile 
with charachange

mi "อ้อ อีกอย่าง ไม่ต้องเรียกชิจังให้เป็นทางการว่า “ฮากามิจิ” หรือ “หัวหน้าห้อง” ตลอดหรอก! เรียกชิจังก็พอแล้ว~!"

stop music fadeout 0.5

show shizu adjust_blush 
with charachange

shi "…"

show misha hips_smile 
with charachange

mi "อะฮ่าฮ่า~! โอเค อันนั้นก็สนิทเกินไป งั้นเรียก “ชิซูเนะ” ดีกว่าเนอะ"

show shizu basic_normal2 
with charachange

shi "…"

show misha hips_grin 
with charachange

play music music_shizune fadein 5.0

mi "อื้ม ๆ ~! “ชิซูเนะ” ก็ได้!"

hi "ฮะ ๆ ได้ ก็เรียกง่ายดี"

"ค่อยโล่งขึ้นมาหน่อย ทั้งคู่ก็ดูเป็นมิตรดี ทำเอาที่ระแวงเมื่อกี้เหมือนเป็นบ้าไปเองเลย\nยิ่งชิซูเนะที่นึกว่าจะเป็นพวกเอาจริงเอาจังอีก"

"ก็ยังดูจริงจังแหละ แต่ก็ไม่ได้ขนาดนั้นมั้ง"

show shizu behind_frown 
with charachange
    
shi "…!"

show misha perky_confused 
with charachange

mi "หืม อ้อ จริงสิ ยังไม่ได้ทำงานเลย! ต้องรีบทำแล้ว เดี๋ยวชิจังจะโกรธ"

hi "งานก็เยอะด้วย ต้องทำแต่เนิ่น ๆ ถ้าอยากให้เสร็จก่อนเลิกคาบ"

show misha hips_laugh 
with charachange

mi "วะฮ่าฮ่า~! เธอพูดถูก!"

show shizu basic_frown 
with charachange

shi "…"

"ชิซูเนะมองมาทางเราสองคนด้วยความร้อนรน อันนี้ไม่ต้องใช้ภาษามือก็รู้"

hi "โอเค ๆ เข้าใจละ"

show shizu basic_normal 
with charachange

shi "…"

show misha perky_smile 
with charachange

mi "เลิกเรียนแล้วไปเดินในโรงเรียนด้วยกันนะ วันนี้วันดี! โอเคนะ~"

"งานก็ทำยาก ทั้งยากแล้วก็เยอะด้วย"

stop music fadeout 6.0

scene bg school_scienceroom
with shorttimeskip

"ทั้งที่เริ่มช้าแต่ก็เสร็จก่อนคนอื่นไม่กี่นาทีอยู่ดี ชิซูเนะกับมิช่านี่เก่งจริง ๆ"

"แต่ทั้งสองคนก็ต่างกัน หัวห้องดูจะใจเย็นและเรียบร้อยเหมือนหน้าตาท่าทาง ส่วนมิช่าดูขี้เล่น\nกับมีความเป็นผู้หญิงมากกว่า แถมยังสมาธิหลุดง่ายด้วย"

"เอาจริง ๆ งานส่วนใหญ่คือทั้งคู่นั่นแหละที่ทำ รู้สึกผิดเลยแฮะ"

play sound sfx_normalbell

"เสียงระฆังจากหอนาฬิกาดังเป็นสัญญาณว่าหมดคาบและพักเที่ยงแล้ว"

scene bg school_hallway3
with locationchange

"ฉันตามมิช่าที่กวักมือให้เดินไปที่โถงแล้วลงบันไดไปเพราะไม่รู้จะทำอะไรดี"


    
#**************************************
label th_A3:

scene bg school_staircase2
with locationchange

"We descend even below the lobby where I met Mutou, down to the bottom floor."

play ambient sfx_crowd_indoors fadein 6.0

scene bg school_cafeteria
with locationchange

"Just like everything in this school, the cafeteria seems too spacious and oddly modern in contrast to the classic exterior."

"Its big windows open to the courtyard, towards the main gate."

show misha hips_grin
with charachange

play music music_ease

mi "It's the cafeteria~!"

"Her enthusiastic statement of the obvious makes people around us stare, but Misha doesn't seem to care so we proceed to the line."

hide misha
with charaexit

"There is a rather long list of menu options, which seems great until I realize that many of them are to accommodate students who need special diets."

"How nice. It almost feels like I'm back at the hospital, eating portions measured with scientific precision to meet the needs of the patients."

"I pick something at random and follow Shizune to a table, sitting opposite of her."

show misha hips_frown at twoleft
show shizu basic_normal at tworight
with charaenter

"As I nibble indifferently at the food I'd rather not eat, Misha pokes me in the side to get my attention and points to Shizune."

show misha perky_smile 
show shizu basic_frown 
with charachange

shi "…"

"I don't understand sign, so the point escapes me."

"Maybe looking at a person who “talks” to you is proper and polite?"

show misha hips_smile 
show shizu behind_blank 
with charachange

mi "Do you want to know something?"

hi "What?"

show misha hips_grin 
with charachange

mi "About anything! We're your guides so you should ask if there is something~!"

label th_choiceA3:
menu:
    with menueffect
    
    hi "Hmm, I wonder…"
    
    "Ask about the library.":
        return m1
    
    "Ask about Shizune's deafness.":
        return m2
    
    "I think I got everything I need to know.":
        return m3        
    
label th_A3a:

hi "Oh, yeah. Is there a library in the school? Lately I've gotten into reading a lot so I'd like to check it out."

"Misha gives the kind of frown that makes it clear she doesn't consider reading a healthy hobby, but then picks up her smile again."

mi "There is~! It's in the second floor, we can show it to you sometime!"

hi "Thanks."

"I return to my food while the girls talk between themselves."



label th_A3b:

"Shizune intrigues me, and I kind of want to ask something about her."

"But I can't really ask about something that personal, can I?"

"Hmm…"

"I can't come up with anything else to ask so I just focus on my food while the girls talk between themselves."



label th_A3c:

hi "I can't think of anything, really."

show misha sign_smile
with charachange

mi "Ooh! That means we've been good guides, doesn't it, doesn't it~?"
 
"…"
 
hi "Eeh… if you say so."

show misha hips_grin 
with charachange

show shizu behind_smile
with charachange
 
"Misha positively beams, and so does Shizune after a quick translation."
 
"I shake my head at their somewhat exaggerated enthusiasm, and shift my focus on the food."



label th_A3d:

"Misha and Shizune sign back and forth very animatedly, throwing sideway glances at me but Misha refrains from translating."

"Maybe they are talking about secret girl stuff or something."

"…"

stop music fadeout 3.0
$ renpy.music.set_volume(1.0, .5, channel="ambient")
stop ambient fadeout 3.0

"I quickly notice a conversation in sign is not enough to fill a silence."

scene bg school_scienceroom
with shorttimeskip

play music music_daily fadein 0.5

"We arrive in the classroom early, but we're not the first."

show hanako emb_downtimid at Transform(xanchor=0.8, xpos=1.0)
with charaenter

"That dark haired girl I noticed before is slumped over her desk at the last row."

show hanako defarms_shock
with vpunch

show hanako defarms_shock at Transform(xanchor=0.6, xpos=1.0)
with charamove

"She jumps a little when Misha crashes into the room with the elegance of a rhino."

"She shrinks deeper into her seat. I can feel her tension all the way from here, as if she were slowly turning into stone just from our presence."

"Misha and Shizune either don't notice or don't mind it, as they walk directly past her to their seats and begin to converse."

show hanako defarms_shock at offscreenright
with charamove

hide hanako
with None

"I'm left wondering about her, even when the classroom slowly fills with other students and finally, the teacher."

"Getting into the rhythm of school feels strange; it's as if my brain remembers how this is done, but my body doesn't."

"Towards the end of the class I start yawning and counting the minutes left."

"I shouldn't be this tired on my first day of school."

"Maybe it's the long time spent in the hospital that made me like this. I'm even feeling physically weak and lifeless."

scene bg school_scienceroom
with shorttimeskip

play sound sfx_normalbell

"Before long, the final bell rings."

"School is finally over for the day."

"Beside me, Misha and Shizune are having a short conversation. After a bit of deliberation, Misha turns to me."

show shizu behind_blank at tworight
show misha perky_smile at twoleft
with charaenter

shi "…"

mi "Unfortunately we can't stay and show you around today, Hicchan. We've got to hurry already, since there is a lot of work for us to do."

show shizu basic_normal2 
with charachange

shi "…"

mi "You'll find your way around here, I'm sure of it."

hi "Ah, wait! The teacher said I'd have to see the nurse. Where do I have to go?"

show shizu behind_smile 
show misha hips_grin 
with charachange

mi "Is that so? We can at least show you that much~!"

mi "Come on, the nurses have their own building, so we have to go outside."    

hide shizu
hide misha
with charaexit

scene bg school_hallway3
with locationchange

"We join the flow of students making their way down the stairwell and outside, with the girls pointing out other senior classrooms in the same hallway as ours."

scene bg school_courtyard
with locationskip

"When we get outside, the girls make their way to the smaller building right next to the school."

"It's built in the same style, so it looks like it's actually a part of the main building."

show shizu behind_smile at tworight
show misha perky_smile at twoleft
with charaenter

shi "…"

mi "This is the auxiliary building here. There's a lot of official and important stuff inside, like the Yamaku Foundation office and all the nurses' offices. They even have a swimming pool!"

hi "How is that official?"

show shizu behind_frustrated 
with charachange

shi "…"

show misha hips_grin 
with charachange

mi "Don't be silly, Hicchan! It's for physical therapy of course."

show misha sign_smile 
with charachange

mi "Anyway, all the nursing staff facilities are in there too. The head nurse's office is on the first floor."

show misha hips_smile 
show shizu behind_smile 
with charachange

mi "You'll be fine from here, right~? We'll be going, then! See you tomorrow!"

hi "Yeah, thanks. Bye."

stop music fadeout 5.0

hide shizu
hide misha
with charaexit

"A whole building for stuff that has nothing to do with the actual education?"

"I guess it's necessary for a place like this."

scene bg school_nursehall
with locationchange

"I walk in, hoping that this really will be only a quick visit like the teacher said."

"On a white door on the left is a green cross with the text “Head Nurse” and a nameplate."

play sound sfx_doorknock2

"A voice from the inside responds to my knock almost immediately, but I can't quite make it out."

"It sounded a bit like an invitation to open the door, so I invite myself further in."

play sound sfx_dooropen

scene bg school_nurseoffice
with locationchange

"The room is not large and it smells strange. A friendly-looking man turns around on his office chair to face me as I enter."

"His desk is neat and tidy, but the bin under the table is overflowing with used medical utensils and there are at least a dozen coffee-cup rings lingering on the desk."

play music music_nurse fadein 0.5

show nurse neutral at center
with charaenter

nk_ "Hello there. What can I do for you today?"

"He is young-looking and sort of rugged, but the dimples in his cheeks wash that impression away when he smiles."

hi "Erm, are you the nurse?"

show nurse grin
with charachange

"He smiles like a person who has heard this very same question hundreds of times."

nk_ "Why yes, I am. It says so on the door, no?"

nk_ "You can call me by my name or just “the nurse” like everyone else."

"Of course. I shake off my confusion, realizing I probably should grab his extended hand.{w} His handshake is rather firm and friendly."

hi "Right… err, I'm a new student and my homeroom teacher told me to come and meet you. My name is Hisao Nakai."

play sound sfx_snap

"His eyes light up with revelation and he snaps his fingers."

show nurse fabulous
with charachange

nk "Oh you're THAT Nakai. I was just reading your file in the morning."

nk "Some kind of chronic arrhythmia and related congenital heart muscle deficiency, right?"

"He gestures me to sit down in a vacant armchair in front of his desk."

hi "Eh, yes."

show nurse neutral
with charachange

nk "Good. Well, you've probably been briefed about the school enough, so I'll just go over this quickly."

nk "We have all kinds of facilities available, mostly physical therapy and such."

nk "There's always someone from my staff around, even at night, so never hesitate to call us if there is a problem."

"The famous twenty-four-hour nursing staff."

hi "Wow, this is like a hospital."

nk "Well, not exactly. For instance, we don't do brain surgery here."

"His joke feels so out of place that I'm left thinking why he even said it."

hi "Yeah… just that it's really weird to have so many medical people at a school."

nk "You'll get used to it."

"I'm not so sure of that myself but I don't let the nurse know it."

nk "Now, let me just find your file again…"

"While he searches for something from his computer and shuffles stacks of papers around, I let my gaze wander around the room."

"It's the epitome of generic, I'd like to say."

"Beige walls and ceiling, dark gray laminate flooring, and all the equipment you'd expect from a school nurse's office."

"Even the ridiculous educational posters are hanging on all four walls, reminding me to eat properly - three times a day and from all the food groups."

show nurse grin
with charachange

"Smiling, the nurse draws a thick file from a stack of similarly thick files and opens it."

nk "So, you already have medication for the arrhythmia, just remember to take your pills every morning and evening or it won't be much help."

show nurse fabulous
with charachange

nk "Apart from that… do you do any sports? Rash stuff like… I don't know, boxing?"

"He grins to his own joke but I don't."

hi "Eh, well. I played soccer occasionally with some classmates."

nk "All right, I'm afraid I'm going to have to recommend you refrain from doing that. At least, for the time being."

hi "Oh."

"My lack of reaction makes him raise an eyebrow, but really, I'm not too bothered by him forbidding me to kick a ball around."

"I guess I never did it out of burning passion for the sport. Just to have something to do."

nk "Any kind of concussion might be very dangerous to your heart, and risking another attack is not a good idea."

nk "Was the previous one caused by a sudden concussion to the chest area? There is no mention of the cause in your papers."

hi "Err… not exactly."

show nurse concern
with charachange

"I sidestep the question acceptably, and he glances at me over his papers, with a more serious expression on his face."

nk "Still, you need to keep your body healthy so some exercise would do you good."

nk "We have physical therapy and such available as I said, but I don't think you really need such heavy measures."

nk "Just get some light exercise regularly."

nk "Brisk walks or even light jogging, jumping rope, that sort of thing. Swimming, maybe? There's a pool here."

hi "So I was told."

show nurse neutral
with charachange

nk "You were? Very good."

nk "At any rate, and I'm sure you've been told this before, you just need to take care not to overexert yourself."

"He wags his finger to emphasize the point. No need really, I've heard this a thousand times already."

show nurse concern
with charachange

nk "Absolutely no unnecessary risks. Take care of yourself."

hi "Okay."

"He goes over my papers one more time and sets them on the desk, obviously content."

show nurse neutral
with charachange

nk "Good. That's it, then. Come meet me if you ever need something."

scene bg school_nursehall
with locationchange

stop music fadeout 2.0

"I'm ushered out before I even realize it. A quick visit, indeed."


#***************************************

label th_A4:

scene bg school_courtyard
with locationchange

play music music_pearly fadein 4.0

"I end up standing in front of the main building and the auxiliary building, although to my eyes, they still look one and the same."

"It's the first real look I get at the other students, so I watch people coming out of the school, going towards the gate or the dorms."

"Everyone seems to know where they are going."

"And I still keep thinking that most of them don't look too special for being students at a special school. Then again, neither do I."

"Does that make me one of them?{w} One of us?"

"…"

"I should go somewhere too, to prevent me from getting lost."

"It's around dinnertime, but I feel tired instead of hungry."

"The weariness in me only grows as I trudge towards the dorms, set a little way apart from the main building complex."

scene bg school_gardens
with locationchange

"There is a garden of sorts between the school and the dorms; shrubbery, flowers and that overbearing smell of fresh cut grass that fills the atmosphere."

"It dawns on my tired mind that the smell feels novel because I haven't been outside at all for so long."

scene bg school_dormext_start at bgleft
with locationchange

"The dorm building is big and made of red brick. Like the others, it feels way too pompous for what it is, so I push forward, going inside."

scene bg school_dormhallground
with locationchange

"It takes more time than necessary to fish out the key I was given from my pocket."

hi "Room one-one-nine…"

"Despite the ornate exterior, the inside of the dorm is fairly new, functional, and boring."

"Just like in the main building, the halls and doors are wide to accommodate wheelchairs. The same goes for the elevators at the ends of the hallways."

"I poke my head around the corner of the common room door."

"Inside a few students are watching the television."

"One nods and gives a quick “hello” before turning back to the TV."

"Seems that only the girls around here are sociable. I suppose that's perfectly fine with me."

"I climb the stairs to the upper floor."

scene bg school_dormhallway
with locationchange

"Here, small corridors branch off from the main hallway."

"Each of these minor halls seems to have a toilet and shower, as well as four rooms."

"About halfway down the hall, I spy room 119."

"The name plates on the rooms adjacent to mine are blank. I guess there are just two of us here."

play sound sfx_doorknock2
stop music fadeout 3.0

"Light shines from below the door of room 117, so I knock lightly."

hi "Hello, is anyone home?"

"From inside, I hear a few movements, then the clicking of way more locks than I thought these doors had. After a moment the door squeaks open."

show kenji tsun at Slide(0.4,0.5,0.5,0.5,0.5)
with charaenter

play music music_kenji fadein 0.5

"A bespectacled boy is standing in the doorway. He is looking at me very intently through his extremely thick eyeglasses."

ke_ "Who is it?"

"Blind? No, at least not completely, why would he have eyeglasses if he was?"

show kenji tsun_close at center
with characlose

"He leans closer to me until our noses are almost touching. His breath stinks of garlic."

hi "Hisao Nakai… I'm moving into the next room. I thought I should introduce my…"

show kenji happy
with charadistant

"His face suddenly brightens in realization, and he stands back upright, thrusting his hand out in a smiling greeting, almost straight to my diaphragm."

ke_ "Oh, 'sup dude? The name's Kenji."

hi "Ah, hi."

"I take Kenji's sweaty hand and shake it, still a little rattled by the sudden change of attitude and vehement welcome."

ke "There were some suspicious-looking people going in and out of your room earlier."

hi "It was probably my parents."

show kenji tsun
with charachange

ke "Your parents? You sure? 'Cause they could've been some other people, too. You can't judge a book by its cover."

"His out-of-place proverb is left hanging between us awkwardly as I try to think of some way to respond."

hi "I'd say the chances are high enough."

"He shudders and makes some exaggerated hand gestures."

show kenji neutral
with charachange

ke "You're a brave man, Hisao."

show kenji tsun
with charachange

ke "Me, I don't think I could trust the chances."

ke "The only one I trust is myself."

hi "Does that mean I shouldn't get to know you, either?"

"He thinks about this for a while."

show kenji neutral
with charachange

ke "A wise decision."

show kenji happy
with charachange

ke "Damn, you are smarter than you look."

ke "Probably."

ke "What do you look like? I hope not smart."

show kenji neutral_close
with characlose

"He squints his eyes and leans closer again, but I lean backwards to dodge it."

show kenji tsun
with charadistant

ke "Never mind, it doesn't matter."

hide kenji
with charaexit

stop music fadeout 3.0

"With that, he turns, fumbles around for a moment in search of the door handle,{w=0.3}{nw}"

play sound sfx_doorslam

extend " and shuts the door behind him."

"I slide the key into the lock of the door marked 119."

scene bg school_dormhisao_ss
with locationchange

play music music_night fadein 1.0

"Bleak beige walls, white linen, a desk made of some type of light wood. Ugly curtains."

"It's no one's room; impersonal, like my hospital room was."

"My bags are sitting at the foot of my bed, looking a lot emptier than they did this morning."

"The closet is sitting open, stocked with my clothes."

"Also, it seems that there are a number of school uniforms hanging there as well."

"A note is pinned to the sleeve of one of the shirts."

window hide

$ written_note("Hi Hicchan. We've unpacked your things and made your bed.\nThey said that if these don't fit then you should go to the office tomorrow.\nIf you have any problems, you can always call us.\n\nLove, Mom and Dad")

window show

"Well, at least I don't have to worry about unpacking."

"I kind of hoped I would have, then there would be something to do."

"It's still too early."

"I put the note down on the desktop and lie down on the bed, feeling drained."

"Lying there makes me want to read something, but I have nothing with me."

"I wonder if the hospital conditioned me for wanting to read whenever I have nothing to do."    

"The restless urge just keeps growing until I have to stand up."

"Maybe it's stress or something."

"I was pretty nervous about it before coming and for the entire day today too. I still am, I think."

"Damn, I have to distract myself somehow, so I won't be this unnatural all the time."

"Tomorrow, I'll go borrow some books from the library."

"Yeah, I'll do that."

"But for now…"

show pills:
    alpha 0.0 xalign 0.5 yanchor 0.5 ypos 0.7 subpixel True
    easein 1.0 ypos 0.5 alpha 1.0
with Pause (1.0)

"The bottles of medications neatly arranged on my night table catch my eye."

"I pick up one and shake it just to hear the contents rattle inside, and then read the glued-on pharmacy label."

window hide

show pills:
    alpha 1.0 xalign 0.5 yanchor 0.5 ypos 0.5 subpixel True
    easeout 1.0 ypos 0.7 alpha 0.0
with Pause (1.0)

hide pills
with None

$ written_note("Hisao Nakai\n\nTwo tablets daily to stay alive", quiet=True)

window show

"It doesn't really say that, but it could just as well."

"It's kinda twisted, having your life depend on chemicals like this. I resent it a little, but what choice do I have?"

"With a sigh, I begin my new daily ritual of taking the right number of pills from each bottle, being careful to check the correct dosages."

"…"

"I lie down again, feeling hollow and uncertain, and after that I keep staring at the blank, unfamiliar ceiling for a long time."

stop music fadeout 8.0

scene bg school_dormhisao_ni
with Dissolve(3.0)

"It doesn't start looking any more familiar, not even after darkness falls and long shadows draw across my room like fingers."

"The sheets feel slightly more comfortable, warm and nest-like against the chill that passes for room temperature here."

"Soon the lighter shade of darkness that is the ceiling looks like every ceiling does at night, and it becomes the only thing I recognize any more."

"The night beckons me to sleep, and I feel the coldness of unfamiliarity and fear creeping up my spine once again."

"I keep drifting further away from the world I knew."    

$ suppress_window_after_timeskip = True

scene black
with shuteye


return

