#### MONDAY

#**************************************
label th_NOP1:
window hide None

scene black
with None

scene bg op_snowywoods
show snow
with Dissolve(2.0)

window show

play music music_serene fadein 2.0

"ลมโชยพัดให้กิ่งไม้เสียดสีกันดังเป็นจังหวะ"

"ที่นี่เป็นที่ที่คู่รักมักมาอยู่ด้วยกันในหน้าร้อน ทั้งต้นไม้ที่ผลัดใบ ใบไม้เขียวขจี และปลอดคน"

"แต่เมื่อเป็นช่วงปลายหน้าหนาวอย่างตอนนี้ก็ไม่ต่างอะไรกับการยืนอยู่ใต้กิ่งไม้แห้ง ๆ"

"ระหว่างรอก็คอยเป่าลมอังแล้วถูมือเพื่อไม่ให้ประสาทชาท่ามกลางอากาศหนาวเย็นไปเสียก่อน"

hi "ต้องรอนานแค่ไหนเนี่ย เหมือนในกระดาษก็บอกว่าสี่โมงนะ"

"ใช่… กระดาษโน้ต… ที่แอบมาแทรกตัวอยู่ในหนังสือคณิต"

"ถ้าพูดถึงวิธีอะไรที่มันเชย ๆ จริง ๆ ชอบแบบที่เป็นจดหมายในตู้ล็อกเกอร์มากกว่านะ แต่อย่างน้อยวิธีนี้ก็ทำให้เห็นถึงความตั้งใจดี"

"หิมะค่อย ๆ ทับถมหนาขึ้นระหว่างที่กำลังคิดถึงกระดาษโน้ตนั้น"

"สิ่งบอกกระแสเวลาที่ไหลผ่านเดียวในโลกอันเอื่อยเฉื่อยนี้คือเกล็ดหิมะที่ร่วงหล่นลงมาจากฟากฟ้าสีขาว"

"ความอ้อยอิ่งของมันทำให้ดูเหมือนว่าเวลาช้าลง"

play sound sfx_rustling

"เสียงย่ำหิมะที่ทำลายความเงียบทำให้ฉันตกใจ มีบางอย่างกำลังมาจากข้างหลัง"

mystery "ฮิ… ฮิซาโอะ มาด้วยเหรอ"

"เป็นคำถามอันแผ่วเบาที่ดูอึกอัก"

"แต่ฉันจำเสียงอันบอบบางนั้นได้ทันที"

"ใจฉันเต้นไม่เป็นส่ำ"

"เป็นเสียงที่ได้ยินมาแล้วหลายร้อยครั้ง แต่ก็เพียงในฐานะคนแอบฟังเท่านั้น"

"ฉันหันไปทางเสียงในฝันนั้น ทำเอาใจเริ่มเต้นแรง"

window hide

$ ksgallery_unlock("evul other_iwanako")
scene ev other_iwanako_start
show snow
with GenericWhiteout(0.5,0.0,5.0)

window show

hi "อิวานาโกะเหรอ เห็นมีโน้ตบอกให้มารอที่นี่… ของเธอใช่มั้ย"

"ให้ตายเถอะ นั่งคิดบทดี ๆ มาทั้งบ่ายแล้วดันได้แค่นี้"

"น่าสมเพช"

"อิวานาโกะ" "อ่า… ใช่ พอดีฝากเพื่อนเอาไปให้น่ะ… ดีจังที่นายเห็น"

"รอยยิ้มที่ทั้งดีใจและเขินอายเล่นเอาเกร็งจนขยับตัวแทบไม่ได้"

stop music fadeout 10.0

window hide

scene bg op_snowywoods
show snow
with GenericWhiteout(0.5,0.0,2.0)

play sound sfx_heartslow
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.7)

with Pause(0.7)

play sound sfx_heartslow
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.7)

window show

"หัวใจเต้นแรงราวกับจะออกมาจากหน้าอกเพื่อรับเธอคนนี้ไว้เสียเอง"

window hide

scene ev other_iwanako
show snow
with GenericWhiteout(0.5,0.0,3.0)

window show

hi "คือ… เอ่อ… แล้ว ทีนี้ยังไงต่อเหรอ…"

"ลมพัดกิ่งไม้อีกครั้ง เสียงที่ดูไร้ระเบียบกลับดูน่าฟัง"

"อิวานาโกะสะดุ้งเล็กน้อยกับลมที่พัดนั้น"

"เมื่อสายลมผ่านไป เธอก็จัดแจงตัวเองราวกับมีความกล้าขึ้นมาใหม่"

"เธอจ้องมองฉัน พลางม้วนผมยาวดำขลับนั้นเล่น"

"ในใจฉันก็ตื่นเต้นตาม"

window hide

scene bg op_snowywoods
show snow
with GenericWhiteout(0.5,0.0,2.0)

window show

"รู้สึกจุกคอหอยไปหมด อยากจะพูดแต่ก็พูดไม่ออก"

"อิวานาโกะ" "ก็นะ…"

window hide

play sound sfx_heartslow
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.4)

window show

"อิวานาโกะ" "…คือว่า เรา…"

stop music fadeout 0.5

window hide

play sound sfx_heartslow
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.2)

with Pause(0.7)

play sound sfx_heartfast
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.2)

window show

"อิวานาโกะ" "… มาคบกันมั้ย…"

"ฉันยืนนิ่งอยู่อย่างนั้นเพราะใจที่เต้นอยู่"

"พอจะพูดอะไรตอบก็รู้สึกเหมือนเส้นเสียงถูกดึงจนขาด"

play music music_tragic fadein 0.05

"อิวานาโกะ" "… ฮิซาโอะ?"

"พยายามจะเปล่งเสียงออกมา แต่กลับได้ความปวดตามแขนมาแทน"

"อิวานาโกะ" "ฮิซาโอะ?!"

window hide

play sound sfx_heartfast
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.8)

window show

"ทั้งตัวขยับไม่ได้ ตาเบิกค้างด้วยความตกใจกลัว"

window hide

play sound sfx_heartfast
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.8)

with Pause(0.15)

play sound sfx_heartslow
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.8)

play sound sfx_heartfast
show heartattack alpha 
with Dissolve (0.1)

hide heartattack alpha
with Dissolve (0.8)

with Pause(0.05)

play sound sfx_heartstop
show heartattack alpha 
with Dissolve (0.1)

stop music fadeout 0.3

show heartattack residual
with Dissolve (0.8)

window show

"อิวานาโกะ" "{b}ฮิซาโอะ{/b}!"

"จู่ ๆ หัวใจก็หยุดเต้น เข่าอ่อนแรงจนทรุดลง"

window hide

show passoutOP1
with None

nvl show Dissolve(0.2)

n "\n\n\n\n\n\n\nโลกรอบตัว—กิ่งไม้ที่ปกคลุม ฟ้าหมองมัว อิวานาโกะที่พุ่งเข้ามา—ต่างดับมืด"

n "\nสิ่งสุดท้ายที่จำได้คือเสียงของอิวานาโกะที่กรีดร้องขอความช่วยเหลือและเสียงกิ่งไม้ที่เสียดสีกันไม่หยุดหย่อน…"

nvl hide Dissolve(3.0)

nvl clear

with Pause (1.0)

scene black
with None

#**************************************
label th_NOP2:

window hide None

scene black
with None

centered "สี่เดือนมาแล้วตั้งแต่หัวใจวายครั้งนั้น" with dissolve

scene bg hosp_room
show sakura
show hospitalmask
with Dissolve (1.5)

$ renpy.music.set_volume(0.5, 0.0, channel="music")
play music music_rain fadein 4.0

nvl show dissolve

n "\n\nช่วงสี่เดือนนั้นแทบจะนับนิ้วได้เลยว่าได้ออกจากโรงพยาบาลโดยไม่มีคนตามคุมกี่ครั้ง"

n "สี่เดือนนั้นถือว่าเป็นเวลาที่นานถ้าได้อยู่แค่กับตัวเอง ถึงได้มีเวลาพอที่จะทำใจกับสถานการณ์ตัวเองได้แบบเหลือเฟือ"

n "โรคหัวใจเต้นผิดจังหวะ"

n "เป็นคำที่ฟังดูไม่น่าอภิรมย์ อยากหนีไปให้ห่าง"

n "เป็นอาการที่พบไม่มากที่ทำให้หัวใจเต้นอย่างผิดปกติหรือเร็วเกินไป และเป็นอันตรายถึงชีวิต"

n "ดูเหมือนว่าจะเป็นมานานแล้วด้วย เห็นบอกว่าปาฏิหาริย์มากที่อยู่มาได้ขนาดนี้โดยไม่เป็นอะไรเลย"

n "ปาฏิหาริย์เลยเหรอ พูดให้รู้สึกดีที่ยังรอดมาประมาณนั้นมั้ง"

n "ซึ่งไม่ได้ช่วยให้รู้สึกดีขึ้นสักเท่าไหร่"

nvl clear

n "\n\n\n\n\nส่วนพ่อแม่ก็ดูเหมือนจะช็อกกว่าฉัน ช็อกจนหัวใจจะวายแทนแล้วมั้ง"

n "\nฉันก็มีเวลาเป็นวัน ๆ ให้ทำใจหมดแล้ว แต่ทั้งพ่อแม่ก็เพิ่งรู้ ณ ตอนนั้นเลย ยอมจะขายบ้านเพื่อจ่ายค่ารักษาให้หายขาดด้วยซ้ำ"

n "\n\nซึ่งแน่นอนว่าไม่มีทางรักษาให้หายขาด"

nvl clear

n "\nเพราะเพิ่งมารู้อาการ… นี้ ถึงต้องอยู่โรงพยาบาลเพื่อพักฟื้นจากการรักษา"

n "ตอนที่มาอยู่ใหม่ ๆ ก็เหมือนมีคนคอยคิดถึงอยู่หรอก…"

n "ห้องมีแต่ดอกไม้ ลูกโป่ง การ์ด เต็มไปหมดอยู่สักสัปดาห์ได้"

n "แต่ผ่านไปไม่นานคนมาเยี่ยมก็ร่อยหรอลง ของฝากอะไรก็เริ่มหายไปจนไม่มีเลย"

n "จนมารู้ว่าที่ส่งการ์ดกับดอกไม้มาอวยพรให้เพราะแค่เป็นงานห้องเฉย ๆ"

n "คนเป็นห่วงจริง ๆ ก็คงมีแหละ แต่ก็นะ ขนาดทีแรกยังแทบไม่มีคนมาเลย ผ่านไปเดือนนึงก็มีแค่พ่อแม่ที่มาเป็นประจำ"

n "อิวานาโกะเป็นคนสุดท้ายที่หายหน้าไป"

n "ผ่านไปหกสัปดาห์ก็ไม่เห็นเธออีกเลย แต่ยังไงตอนมาเยี่ยมก็แทบไม่ได้คุยกันอยู่แล้ว"

n "เราไม่ได้คุยเรื่องวันหิมะตกนั้นอีกเลย"

nvl clear

n "\nโรงพยาบาลเหรอ"

n "ไม่ใช่ที่ที่อยากอยู่นักหรอก"

n "ทั้งหมอทั้งพยาบาลก็รู้สึกเหมือนเป็นคนแปลกหน้ายังไงไม่รู้"

n "คงเพราะรีบแล้วก็มีคนไข้อีกเป็นล้านรออยู่ละมั้ง แต่ก็รู้สึกอึดอัดเหมือนกัน"

n "ช่วงเดือนแรก ๆ พอเห็นหมอหัวใจก็คอยถามอยู่ว่าจะออกได้สักเมื่อไหร่"

n "เขาไม่เคยตอบอะไรตรง ๆ เลย บอกแค่ว่ารอดูว่าการรักษากับการผ่าตัดจะได้ผลมั้ย"

n "\nเลยมานั่งมองแผลเป็นหลังผ่าตัดที่ค่อย ๆ เปลี่ยนรูปไปเรื่อย ๆ มองเหมือนเป็นตัวกาลกิณีสักอย่าง"

n "ก็ยังถามเขาเรื่องออกจากโรงพยาลอยู่นั่นแหละ ถึงจะไม่ได้คาดหวังอะไรจากคำตอบที่ไม่เคยมีจากรอบก่อน ๆ แล้ว\nแต่ถ้ายังเลี่ยง ๆ ไปมาแบบนี้ก็อาจจะพอมีหวังอยู่"
    
nvl clear

n "\n\n\n\nพอถึงจุดนึงก็ไม่ได้ดูทีวีเลย ไม่รู้หรอกทำไม แต่ก็ไม่ได้ดูแล้ว"

n "อาจจะเพราะดูไปก็ไม่ได้รู้สึกเป็นอิสระจากสถานการณ์ตอนนี้เท่าไหร่"

n "\nเลยหันมาอ่านหนังสือแทน ที่โรงพยาบาลมี “ห้องสมุด” อยู่ ถึงจะเหมือนเป็นแค่ห้องเก็บหนังสือเฉย ๆ ก็เถอะ ก็ค่อย ๆ อ่านไปทีละกอง พออ่านกองนึงหมดแล้วก็ไปเอามาใหม่"

n "ถึงได้รู้ตัวว่าชอบอ่านหนังสือ ออกจะเสพติดด้วยซ้ำ เริ่มรู้สึกเหมือนพอไม่มีหนังสือติดมือแล้วเหมือนขาดอะไรไป"

n "\nฉันชอบอ่านเรื่องราว"

nvl clear

n "\nนั่นแหละชีวิตฉัน"

n "\nวัน ๆ เริ่มเหมือนกันไปหมด ต่างก็แค่หนังสือที่อ่านกับสภาพอากาศข้างนอก เหมือนเวลาเป็นก้อนอะไรใหญ่ ๆ สักอย่างที่ฉันติดอยู่ข้างใน ไม่ได้เคลื่อนไปไหน"

n "บางทีสัปดาห์นึงผ่านไปยังไม่รู้ตัวด้วยซ้ำ"

n "บางทีก็มานั่งคิดว่าวันนี้วันอะไร"

n "แต่ก็มีบางทีที่ทุกอย่างเข้ากระทบประสาทรับรู้ของฉันอย่างหนักหน่วงผ่านกำแพงความเมินเฉยที่ฉันสร้างขึ้นมา" 

n "รู้สึกเหมือนจับหนังสือก็เจ็บ ในอกก็แน่นไปหมดจนต้องวางหนังสือแล้วนอนมองเพดานอยู่เฉย ๆ สักพักเหมือนจะร้องไห้" 

n "แต่ก็เป็นแบบนั้นไม่บ่อย"

n "\nแถมร้องไห้ไม่ออกด้วยซ้ำ"

$ renpy.music.set_volume(1.0, 1.0, channel="music")

nvl hide dissolve

nvl clear

window show

"วันนี้หมอเข้ามายิ้มทักทาย เหมือนจะดูตื่นเต้นแต่ก็ไม่ ออกแนวพยายามร่าเริงแทนฉันมากกว่า"

"พ่อแม่ก็มาด้วย ไม่เห็นหน้ามาสองสามวันแล้วเหมือนกัน ทั้งคู่ก็ดูจะแต่งตัวมาดีด้วย แต่เนื่องในโอกาสอะไร\nไม่ได้จะจัดงานเลี้ยงนี่"

"หมอหัวใจจะชอบทำแบบนี้ประจำ จะอยู่สักพักก่อน แล้วก็จัดเอกสาร\nแล้วก็วางไว้เหมือนจะให้ที่ทำอะไรไร้สาระเมื่อกี้มีสาระขึ้นมา"

"แล้วก็มานั่งสบาย ๆ ตรงริมเตียงที่อยู่ถัดกัน มองตาอยู่สักพักหนึ่ง"

"หมอ" "สวัสดีฮิซาโอะ วันนี้เป็นไงบ้าง"
   
"ฉันไม่ตอบ แต่ยิ้มน้อย ๆ กลับไปให้"

"หมอ" "เธอน่าจะกลับบ้านได้แล้วนะ หัวใจก็แข็งแรงขึ้นแล้ว ถ้าระวังตัวอะไรอีกหน่อยก็ใช้ได้แล้วละ"

"หมอ" "หมอสั่งยาไว้ให้แล้ว เดี๋ยวจะให้ใบสั่งยากับพ่อเธอไว้นะ"

"หมอให้กระดาษใบหนึ่งกับพ่อ ซึ่งสีหน้าดูเครียดขึ้นมาทันทีที่กวาดตาอ่าน"

"พ่อ" "เยอะมาก…" 

"ฉันหยิบมาจากพ่อแล้วอ่านบ้าง ทั้งตัวชาวาบ จะให้ว่ายังไงล่ะเนี่ย"

$ renpy.music.set_volume(0.5, 2.0, channel="music")

scene white
with Dissolve(2.0)

show wallodrugs:
    xalign 0.0 subpixel True
    easein 25.0 xalign 1.0


"รายการยายาวเหยียดที่จ้องมองฉันมาจากกระดาษดูจะมากมายเกินไป ปนระคนกันอยู่ในทะเลตัวหนังสือ"

"บ้าไปแล้ว"

"ผลข้างเคียง อาการไม่พึงประสงค์ ข้อห้ามใช้ ขนาดยา ทั้งหมดเรียงบรรทัดกันเน้นย้ำอย่างหนักแน่น"

"พยายามอ่านแล้วแต่ก็ไร้ผล"

"ไม่เข้าใจอะไรสักอย่างเลย ยิ่งคิดก็ยิ่งเวียนหัว"

"ทั้งหมดนี่… ทุกวัน ไปตลอดชีวิต น่ะนะ"

$ renpy.music.set_volume(1.0, 1.0, channel="music")

scene bg hosp_room
show sakura
show hospitalmask
with fade

"หมอ" "หมอเกรงว่านี่คือสิ่งที่ดีที่สุดที่หมอจะทำได้แล้ว"

"หมอ" "แต่ว่ายาใหม่ ๆ ก็ถูกพัฒนาเรื่อย ๆ อยู่แล้ว ถ้านาน ๆ ไปบางตัวจะหายไปบ้างก็คงไม่แปลกใจ"

"นาน ๆ ไป… อันนี้คือจะให้ใจชื้นขึ้นหรืออะไร ถ้าไม่พูดอะไรเลยยังจะดีกว่ามั้ง…"

"หมอ" "แล้วก็ หมอคุยกับพ่อแม่เธอแล้วนะว่าไม่ต้องกลับไปเรียนที่เดิมแล้วจะดีกว่า"

"อะไรนะ!"

"พ่อ" "ใจเย็นก่อนนะฮิซาโอะ ฟังหมอเขาพูดก่อน…"

"ใจเย็นเหรอ ดูที่พูดนี่ก็คงรู้แล้วมั้งว่าจะไม่เอาด้วย นี่จะได้โฮมสคูลเหรอ"

"แต่ความไม่สบายใจใด ๆ ที่แสดงออกมาก็ถูกเมิน"

"หมอ" "หมอรู้ว่าการศึกษาของเธอนั้นสำคัญ แต่ว่าถ้าไปโดยไม่มีคนคอยดูแลคงไม่ดีเท่าไหร่"

"หมอ" "อย่างน้อย ๆ ก็จนกว่าจะแน่ใจว่ายาที่สั่งไปลงตัวแล้ว"

"หมอ" "ก็เลยคุยกับพ่อแม่เธอเรื่องย้ายโรงเรียน"

"หมอ" "จะได้ย้ายไปที่โรงเรียนยามากุที่จัดตั้งมาสำหรับนักเรียนพิการ"

"พิการเหรอ อะไรนะ นี่ฉัน…"

"หมอ" "มีทีมพยาบาลอยู่ตลอดยี่สิบสี่ชั่วโมง แถมเดินทางไม่กี่นาทีก็ถึงโรงพยาบาลใหญ่ชั้นนำด้วย\nนักเรียนส่วนใหญ่ก็อยู่หอในนั่นแหละ"

"หมอ" "ก็ไม่ต่างอะไรกับโรงเรียนกินนอนหรอก ออกแบบมาให้นักเรียนมีอิสระพอตัวไปพร้อม ๆ กับที่มีคนคอยดูอยู่ตลอดด้วย" #possibly longest text for Thai before slash n

"อิสระเหรอ ยังไงมันก็โรงเรียนสำหรับคนพิการนั่นแหละ อย่าเลี่ยงไปมาสิ"

"ถ้า “อิสระ” ขนาดนั้นจริง ๆ จุดขายก็คงไม่ใช่ทีมพยาบาลยี่สิบสี่ชั่วโมงกับความใกล้โรงพยาบาลหรอก"

"พ่อ" "ซึ่งก็ต้องถามความเห็นลูกก่อนอยู่ดีน่ะนะ แต่ว่า… แม่กับพ่อคงอยู่โฮมสคูลให้ไม่ได้"

"พ่อ" "แม่กับพ่อก็ไปดูมาเมื่อสองสามอาทิตย์ก่อนแล้ว ลูกน่าจะชอบนะ"

"โอเค คงไม่มีทางเลือกจริง ๆ นั่นละ"

"หมอ" "ถ้าเทียบกับโรคหัวใจอย่างอื่น คนที่มีอาการอย่างเธอปกติก็อยู่ได้นานเลย\nเป็นโอกาสอันดีที่จะได้กลับมาเรียนด้วย สักวันเธอก็ต้องหางานทำอีก"

"ไม่ใช่โอกาสสักหน่อย อย่าเรียกว่าโอกาสเลย โอกงโอกาสอะไร"

"หมอ" "ได้กลับเข้าโรงเรียนแบบนี้ต้องตื่นเต้นสิ เห็นเธออยากกลับไปเรียนนี่ ถึงจะไม่ใช่ที่เดิมก็เถอะ…"

"โรงเรียนพิเศษเหรอ นี่จะ…"

"ตบหน้ากันหรืออะไร อยากบอกแบบนั้นแหละ เหมือนซ้ำเติมกันอีก"

"พ่อ" "ไม่ใช่อย่างที่ลูกคิดหรอก นักเรียนที่นั่นเขาต่างก็กระตือรือร้นกันในแบบของตัวเองดี"

"พ่อ" "เป็นโรงเรียนเพื่อคนที่ยังพอใช้ชีวิตเรียนรู้อะไรได้ที่อาจจะต้องการความช่วยเหลือ… สักหน่อย"

"หมอ" "พ่อเธอพูดถูก ศิษย์เก่าที่นั่นหลายคนจบไปก็ทำอะไรหลายอย่าง คนเราเอาความพิการมารั้งไว้ไม่ได้หรอก"

"หมอ" "เพื่อนร่วมงานหมอคนนึงที่อยู่อีกโรงพยาบาลก็จบจากที่นั่น"

"สนที่ไหนล่ะ คนเราเอาความพิการมารั้งไว้ไม่ได้เหรอ ก็ความพิการนั่นแหละที่จะรั้งไว้ไม่ใช่เหรอ"

"ไม่ชอบเลยที่เรื่องใหญ่แบบนี้แต่มีคนมาคิดแทนให้ แต่ทำไงได้ ชีวิต “ปกติ” คงไม่มีอีกแล้วละ"

stop music fadeout 10.0

"ตลกดี เคยคิดอยู่ว่าชีวิตที่ผ่านมาดูน่าเบื่อ แต่ทีนี้กลับอยากเป็นเหมือนเดิม"

"อยากจะประท้วง อยากจะโทษตัวเองที่ไม่มีอาการช็อกหรือเป็นลมอะไรเลย จะตะโกนอะไรก็ได้\nตะโกนว่ายังกลับไปเรียนที่เดิมได้ แต่ไม่มีเลย"

"ไม่ได้พูดอะไรเลย เพราะรู้ว่าทำไปก็เปล่าประโยชน์"

"ฉันมองไปรอบ ๆ ห้องด้วยความรู้สึกหมดอาลัยตายอยากกับทุกสิ่ง ทั้งโรงพยาบาล ทั้งหมอ ทั้งโรคนี้ ทุกอย่าง\nไม่มีอะไรที่จะทำให้รู้สึกต่างออกไปเลย"

"แม้รู้ดีว่าไม่มีทางเลือกแล้ว แต่พอคิดว่าต้องไปโรงเรียนคนพิการ… จะเป็นยังไงกัน\nพยายามจะนึกภาพในแง่บวกก็นึกแทบไม่ออก"

"แต่ขอลองดูสักตั้ง"

"เริ่มใหม่ทั้งหมดก็ไม่แย่"

"พอคิดอย่างนั้นแล้วก็พอจะมีแรงไปต่อ อย่างน้อยก็ยังมีบางอย่างให้มองอยู่ ถึงจะเป็น “โรงเรียนพิเศษ”\nแต่ก็เป็นการเริ่มต้นใหม่ทั้งหมด ชีวิตฉันก็ยังไม่จบสิ้นสักหน่อย ถ้าเอาแต่คิดลบแล้วยอมแพ้ไปก็คงใช่ที่"

"อย่างน้อย ๆ ก็ลองดูก่อนว่าชีวิตใหม่ของฉันจะเป็นยังไง"

window hide



#**************************************
label th_A1:

window hide None

scene bg school_gate
with Dissolve(3.0)

play music music_happiness

window show

"ประตูดูจะหรูหราเกินหน้าที่ไปมาก"

"อันที่จริง ประตูส่วนใหญ่ก็เป็นอย่างนั้นอยู่แล้ว แต่บานนี้ดูจะหรูเป็นพิเศษ"

"อิฐสีแดง เหล็กดัดประณีตกับปูนสีเทา ดู ๆ แล้วไม่น่าเข้าเลย"

"นึกสงสัยอยู่เหมือนกันว่าประตูโรงเรียนต้องเป็นแบบนี้หรือเปล่า แต่ก็ไม่รู้เหมือนกัน คงไม่มั้ง"

"แต่ก็ไม่อยากมาพินิจประตูให้เสียเวลานานจึงเดินเร็วเข้าไป ซึ่งทำให้รู้สึกดีอย่างเหลือเชื่อ"

"การก้าวไปข้างหน้านั้นทำให้รู้สึกดี"

scene bg school_courtyard
with locationchange

"แล้วเดินมายังอาคารหลักของโรงเรียนยามากุด้วยการเดินเร็วนั้น ตอนนี้อยู่ตัวคนเดียว\nเพราะพ่อแม่กำลังย้ายของเข้าหอให้ แถมจะมีคนรอฉันอยู่ด้วย"

"สนามโดยรอบมีหญ้าโตเขียวขจี"

"ซึ่งดูไม่เหมือนสนามโรงเรียนทั่ว ๆ ไป เหมือนสวนสาธารณะมากกว่า ทั้งทางเดินที่ตัดผ่านตามต้นไม้ \nกลิ่นหญ้าที่ตัดใหม่ ๆ แล้วก็อะไร ๆ ที่ทำให้นึกถึงสวนสาธารณะ"

"คำจำพวก “สะอาด” และ “ปลอดเชื้อ” แวบเข้ามาจนเสียวสันหลังวาบ"

"ฉันพยายามสลัดคำเหล่านั้นออก ต้องพยายามเปิดใจไว้ นี่คือชีวิตใหม่ของฉัน ไม่ว่ายังไงก็ต้องไปต่อ"

"คอยบอกตัวเองอย่างนั้น"

"ตึกใหญ่สองสามตึกตั้งอยู่เบื้องหลังกลุ่มใบไม้เหล่านั้น ที่ดูใหญ่และมากเกินกว่าจะเป็นโรงเรียน"

"ทุกอย่างดูแปลก ๆ ต่างไปจากที่คิดเอาไว้"

"เป็นภาพที่ดูคุ้นเคย แต่ดันทำให้รู้สึกประหลาด ทั้งที่รู้แล้วว่าที่นี่จะเป็นโรงเรียนใหม่ของฉัน\nแต่ในหัวเหมือนจะบอกว่าไม่ใช่"

"ไม่รู้ว่าความรู้สึกนี้เป็นเพราะภาพจำกับโรงเรียนสำหรับคนพิการหรือเปล่า"

"แต่พูดถึงคน ก็ไม่เห็นมีสักคน แอบน่ากลัวเหมือนกัน"

"อยากจะให้มีสักคนเอาไว้อยู่เป็นตัวเป็นตนแทนความรู้สึกเหมือนอยู่คนละมิติแบบที่เป็นตอนนี้"

"เสียงใบไม้ไหวกับลมและหญ้าเขียวขจีดึงความสนใจฉัน"

"เห็นแล้วก็นึกถึงโรงพยาบาลอีก ยิ่งชอบบอกว่าที่ทาสีเขียวเพราะเป็นสีที่สงบดี"

"แต่ทำไมยังรู้สึกไม่สงบ ทั้งที่สีเขียวเยอะขนาดนี้"

"…"

"ต่อเมื่อมายืนหน้าอาคารหลักที่ตั้งตระหง่าน จึงนึกขึ้นได้ว่าทำไมอึกอักอยู่ที่ประตูเสียนาน"

"เพราะนั่นคือโอกาสสุดท้ายที่ฉันจะถอนตัว ถึงจะถอนตัวไปแล้วก็ไม่มีชีวิตให้กลับไปใช้อีกก็เถอะ"

"แต่ก็ ถ้าเข้ามาเมื่อไหร่ก็ไม่มีทางกลับไปได้แล้ว"

"ฉันเปิดประตูเข้าไปอย่างประหม่าพร้อมความคิดในหัวเมื่อครู่นั้น"

scene bg school_lobby
with locationchange

"ชายตัวสูงที่ท่ายืนแปลก ๆ คนหนึ่งหันมามองฉัน ซึ่งก็ไม่แปลกเพราะทั้งโถงมีแค่เราสองคน"

show muto normal at center
with charaenter

mu_ "เธอคือ… นิ… นา… นิกิใช่มั้ย"

hi "นากาอิครับ"

show muto smile
with charachange

mu_ "เธอน่ะเอง เยี่ยม ฉันคือครูประจำชั้นเธอนะ สอนวิทยาศาสตร์ ชื่อมุโต้"

mu "ยินดีต้อนรับ"

"พวกเราจับมือกันอย่างพอเหมาะพอดีแล้วเขาก็มองที่นาฬิกาข้อมือ"

show muto irritated
with charachange

mu "เห็นหัวหน้าทีมพยาบาลบอกให้เธอแวบไปตรวจร่างกายครั้งแรกหน่อยด้วย แต่ไว้ทีหลังแล้วกัน"

hi "ได้เหรอครับ"

show muto normal
with charachange

mu "ได้สิ ช่วงบ่าย ๆ น่าจะได้ ตอนนี้ต้องพาเธอไปแนะนำตัวกับชั้นเรียนก่อน น่าจะรอกันอยู่"

"รออยู่เหรอ ไม่ค่อยชอบเป็นจุดศูนย์กลางความสนใจเท่าไหร่ แต่สถานการณ์อย่างนี้ก็คงช่วยไม่ได้ละนะ"

"แต่พอไม่รู้ว่ามีอะไรรออยู่แล้วก็รู้สึกประหม่ายังไงไม่รู้"

"คิดไปคิดมาก็เกือบลืมฟังว่าครูพูดอะไรอยู่"

label th_choiceA1:
menu:
    with menueffect

#choice:
    mu "จะแนะนำตัวหน้าห้องมั้ย"

    "ทำไมล่ะครับ":
        return m1
        
    "ได้ครับ":
        return m2

label th_A1a:

hi "ทำไมล่ะครับ จำเป็นต้องทำเหรอครับ"

mu "ไม่จำเป็นหรอก ถึงได้ถามก่อนนี่แหละ"

hi "โอเคครับ"

mu "งั้นก็ไปกัน"



label th_A1b:

hi "ได้ครับ แต่ไม่ใช่ว่าต้องแนะนำตัวอยู่แล้วเหรอครับ"

mu "ก็ใช่ แต่บางคนก็ไม่อยากเป็นจุดศูนย์กลางความสนใจหรอก"

"ฉันก็คงเป็นคนพวกนั้นด้วย แต่อย่างน้อยเห็นหน้าค่าตากันครั้งแรกก็คงต้องทำให้ดีหน่อย"

hi "นั่นสิครับ แต่ผมได้ครับ"

mu "งั้นก็ไปกัน"



label th_A1c:

scene bg school_staircase2
with locationchange

"ใจที่เต้นอยู่ในอกระหว่างที่เดินขึ้นบันไดตามครูไปคอยเตือนถึงอาการที่เป็นอยู่"

scene bg school_hallway3
show muto normal at center
with locationchange

"ห้องที่สามบนชั้นสามมีป้ายบอกว่าสำหรับห้อง 3-3"

play sound sfx_dooropen

"มุโต้เปิดประตูเข้าห้องไป"

show muto normal at Alphaout(0.5), Slide(0.5,0.5,0.4,0.5,0.5,_ease_out_time_warp)
with Pause (0.5)

hide muto
with None

mu "อรุณสวัสดิ์ทุกคน ขอโทษที่มาช้าอีกแล้วนะ"

stop music fadeout 2.0

"แวบหนึ่งนึกลังเลยืนชะงักอยู่ที่ประตู"



#**************************************
   
label th_A2:

scene bg school_hallway3
with None

"อา ตั้งสติหน่อย! นี่คือก้าวสำคัญ ยังไง… คิดมากไปตอนนี้ก็คงไม่ได้อะไรขึ้นมา"

scene ev hisao_class_start
with fade

play music music_normal fadein 0.5

"ระหว่างที่เดินตามครูเข้าห้องก็มองไปรอบ ๆ ส่วนหนึ่งก็เพราะจะได้ไม่ต้องสบสายตาที่ต่างอยากรู้ของคนในห้อง"

"เป็นห้องที่ค่อนข้างกว้าง เพดานสูงมาก ๆ โต๊ะก็ตั้งห่างกัน"

"กระดานกินที่ทั้งผนังด้านหนึ่ง หน้าต่างทรงสูงก็ทำให้ห้องดูกว้างไปอีก"

"โต๊ะนักเรียนก็เป็นโต๊ะธรรมดาที่มีที่เก็บของอยู่ข้างใต้ เก้าอี้ก็ทำจากไม้กับโครงเหล็ก เรียบง่ายและใช้งานดี"

"ฉันมายืนอยู่ที่หน้าห้องแล้วมองคนอื่น ๆ ทุกคนก็ดูปกติเหมือนนักเรียนทั่ว ๆ ไป\nแต่ถ้างั้นจริงก็คงไม่มาอยู่ที่นี่หรอกแหละนะ"

scene ev hisao_class_move
with None

"ก็คงเหมือนฉันที่มีอาการผิดปกติสักอย่าง แค่ว่าอาจจะไม่ได้เห็นชัดขนาดนั้น\nทว่าก็ดันไปเห็นผู้หญิงคนหนึ่งที่นิ้วโป้งข้างขวาหายไป ดูแล้วก็หลอนหน่อย ๆ"

"ถึงปกติคนจะฟังเวลามีคนพูดถึงตัวเอง แต่ก็แทบไม่ได้ฟังตอนที่ครูแนะนำตัวฉันกับคนในห้อง"

"ฉันเหลือบไปเห็นคนหนึ่งที่มีผมสีดำที่มองมาทางนี้ ผู้หญิงผมตรงยาวที่ค่อนข้างสะดุดตา\nพอเห็นว่าฉันมองอยู่ก็เอามือปิดหน้าเหมือนกับว่าทำแล้วจะล่องหนได้"

"แล้วก็มีผู้ชายคนหนึ่งที่มีไม้เท้าพิงอยู่กับตู้ล็อกเกอร์ในห้อง แปลกดีเหมือนกันที่เห็นคนใช้ไม้เท้าแต่เด็ก"

"แล้วก็ผู้หญิงอีกคนที่ขยับมือไปมาแปลก ๆ อยู่ ภาษามือมั้ง เธอเหลือบมองลอดแว่นมาก่อนจะกลับไปทำอะไรต่อ"

"ดูน่ารักดี ผู้หญิงผมสีชมพูที่ดูร่าเริงที่อยู่ข้าง ๆ ก็เหมือนกัน เตะตามาแต่ไกล\nยังงงอยู่เหมือนกันว่าทำไมตอนเดินเข้ามาถึงไม่เห็น…"

mu "…ต้อนรับเพื่อนใหม่ด้วย"

"พอเขาตบมือทุกคนก็ตบตาม ยกเว้นผู้หญิงคนหนึ่งที่แถวหน้าที่มีมือเดียว\nฉันตกใจนิดหน่อยแต่ก็ทำเป็นโค้งตัวสำหรับเสียงปรบมือที่เกินจะรับไว้นี้"


label th_A2a:

"พอเสียงปรบมือสิ้นความเงียบที่ไม่มีใครอยากทำลายก็เข้าปกคลุม"

"ครูก็นึกได้ว่าต้องพูดอะไรต่อ อ้าปากทำเสียงงึมงำอยู่แล้วก็เงียบไป แล้วก็แนะนำตัวฉัน"

"ดูไม่มีใครสนใจเท่าไหร่"

"รู้งี้ยอมแนะนำตัวเองไปก็ดี"

"คงเพราะเพิ่งนึกได้ว่ายังไม่ได้รู้จักฉันเลย ครูก็ได้แต่พูดชื่อฉัน ซึ่งก็พูดผิด แล้วให้ฉันเขียนชื่อบนกระดาน"

"ฉันก็เขียนแล้วหันหน้ากลับไปพร้อมความรู้สึกกระอักกระอ่วน"


label th_A2b:

"ทุกคนที่พร้อมใจกันเงียบเป็นสัญญาณว่าฉันต้องเปิดปากได้แล้ว"

hi "เอ่อ… ผมฮิซาโอะ นากาอิ"

"แล้วไงอีก"

hi "ชอบอ่านหนังสือกับเตะบอล ถึงจะเพิ่งมาใหม่ แต่ก็ฝากเนื้อฝากตัวด้วยนะครับ"

"แล้วไงอีก"

"ทำตัวน่าเบื่อชะมัด ใคร ๆ เขาก็แนะนำตัวกันแบบนี้ทั้งนั้น ต้องพูดอะไรที่ตื่นเต้นกว่านี้หน่อยสิ"

"สุดท้ายก็ไม่ได้พูดอะไร ส่วนครูก็รับไม้ต่อไป"

"แต่ทุกคนก็ดูจะพอใจถึงแทบจะไม่ได้พูดอะไร ผู้หญิงสองสามคนกระซิบกระซาบกัน ก็ยังดีละมั้งที่ออกมาเป็นแบบนี้" 

"…"


label th_A2c:

scene ev hisao_class_end
with None

"ฉันคอยฟังครูที่พูดเรื่องเพื่อนใหม่ สายตาก็มองไปรอบ ๆ ห้อง"

"ทุกคนดูจะตั้งใจฟัง และพอครูพูดจบทุกคนก็ปรบมืออีกครั้ง ซึ่งก็ดูแปลก ๆ"

"คราวนี้ผู้หญิงที่อยู่แถวหน้าปรบมือแล้ว แต่เสียงก็ตัน ๆ เพราะเป็นแค่มือเดียวตบอยู่กับข้อมืออีกข้างที่มีผ้าพันอยู่" 

"เห็นแล้วก็แอบรู้สึกผิดเหมือนกัน"

scene bg school_scienceroom at bgright
show muto normal at center
with fade

mu "วันนี้จะทำงานกลุ่มนะ จะได้มีโอกาสคุยกับคนอื่นด้วย โอเคมั้ย"

hi "ครับ ได้ครับ"

show muto smile
with charachange

mu "ดี งั้นเธออยู่กับฮากามิจิแล้วกันนะ เธอเป็นหัวหน้าห้องน่ะ"

mu "เผื่อมีอะไรสงสัยจะได้ตอบให้ได้ คงไม่มีใครเหมาะกว่านี้แล้วนี่นะ"

hide muto
with charaexit

"จะไปรู้เหรอ"

"ครูก็แจกงานให้แล้วบอกว่าให้จับกลุ่มกันสามคน"

"ถึงพึ่งรู้ตัวว่ายังไม่รู้เลยว่าฮากามิจิคือคนไหน ความรู้สึกช้าเกิน ครูก็เหมือนจะเห็นว่ากำลังงงอยู่"

mu "อ้อ จริงสิ ฮากามิจิคือคนนั้นนะ ชิซูเนะ ฮากามิจิ"

show misha perky_smile at center
with charaenter

"พอถูกเรียกชื่อ สาวผมสีชมพูที่ดูร่าเริงตาสีทองคนนั้นก็โบกมือให้ ฉันเข้าไปนั่งข้างเธอตรงข้างหน้าต่าง"

hi "ไง เธอฮากามิจิใช่มั้ย ยินดีที่ได้รู้จัก"

stop music fadeout 1.0

show misha cross_laugh
with charachange

mi_shi "ฮ่าฮ่าฮ่า~!"

"หา? อยู่ ๆ ก็หัวเราะไม่ทันตั้งตัว"

show misha hips_grin
with charachange

mi_shi "ยินดีที่ได้รู้จักเช่นกัน!{w=0.5} แต่~!"

mi_not_shi "ยินดีที่ได้รู้จักเช่นกัน! แต่~!{fast} ฉันไม่ใช่ฮากามิจิ ฉันมิช่า คนนี้ต่างหากฮากามิจิ ชิจัง~!"

play music music_shizune fadein 1.0

show misha hips_grin at twoleft
show bg school_scienceroom at center
with charamove

show shizu basic_normal at tworight
with charaenter

"มิช่าชี้ไปที่ผู้หญิงข้าง ๆ เธอพลางหัวเราะคิกคัก เป็นคนที่เห็นใช้ภาษามือเมื่อกี้ เหมือนจะจ้องฉันมาตลอดเลย\nเธอพยักหน้าดูไม่ยี่หระอะไร เพียงรับรู้ว่ามีฉันอยู่ด้วย... แต่ก็แค่นั้น"

"ผมสั้นที่หวีเรียบแปล้ แว่นทรงรีที่วางอยู่บนจมูกบาง กับนัยน์ตาสีน้ำเงินที่ดูทั้งสนใจกับเบื่อเล็กน้อยสลับไปมาตลอด"

hi "ยินดีที่ได้รู้จัก"

show shizu behind_blank 
with charachange

shi "…"

show misha perky_smile 
with charachange

show misha sign_smile 
with charachange

show misha perky_smile 
with charachange

"เธอมองขวับไปทางมิช่าที่ยิ้มและทำมือไปมา"

show shizu adjust_happy 
with charachange

show shizu behind_smile 
with charachange

"ฮากามิจิพยักหน้าและทำมือไปมาบ้าง"

"ชักสงสัยว่าครูอำกันเล่นหรือเปล่า ทั้งที่บอกว่า “มีโอกาสคุยกับคนอื่นด้วย” กับ “มีอะไรสงสัยจะได้ตอบให้ได้”"

show misha hips_smile 
with charachange

mi "ยังงง ๆ อยู่ใช่มั้ย ๆ แต่ก็ไม่แปลกหรอกที่จะนึกว่าฉันคือชิจัง!"

mi "ชิจังเขาหูหนวกน่ะ ฉันก็เลยคอยเป็นคนแปลคำพูดให้"

show misha hips_grin 
with charachange

mi "เหมือนล่ามน่ะแหละ~! เมื่อกี้บอกว่า ยินดีที่ได้รู้จักเช่นกันนะ!"

show shizu basic_normal2 
with charachange

shi "…"

show misha perky_smile 
with charachange

mi "นายเป็นเด็กใหม่ใช่มั้ย เอ่อ ชิจัง ก็ต้องใช่สิ! ถ้าไม่ใช่ก็คงไม่ไปยืนอยู่หน้าห้องเฉย ๆ หรอก ใช่มั้ย ๆ ~!"



label th_A2d:

mi "ดูเป็นคนที่น่าสนใจดีใช่มั้ยล่ะ~!"



label th_A2e:

"มิช่ามองมาทางฉันด้วยสีหน้าแปลก ๆ แล้วพูดต่อ"

mi "ตอนนี้ยังไม่รู้จักมาก แต่เดี๋ยวก็คงรู้แหละ"

"รู้งี้แนะนำตัวเสียก็ดี อย่างน้อยได้พูดอะไรก็คงดีกว่าปล่อยให้ครูพูดเอื่อย ๆ กับพูดชื่อผิดแบบนั้น"



label th_A2f:

mi "รู้มาว่าจะมีนักเรียนใหม่มา แต่ไม่นึกว่าจะมาวันนี้ เร็วจัง! ฮิจังใช่มั้ย"

"ฮิจัง… เหรอ"

show misha hips_grin 
with charachange

mi "ช่าย~! เหมาะดีออก"

"เมื่อกี้ได้พูดด้วยเหรอ ตกใจหมด ไม่ชอบชื่อนั้นเลยแฮะ"

hi "เหมาะตรงไหน"

show misha cross_grin 
with charachange

mi "เหมาะสิ~! หน้าตาก็เหมือนที่คิดไว้เปี๊ยบเลย!"

show shizu adjust_smug 
with charachange

shi "…"

show misha cross_laugh 
show shizu adjust_happy 
with charachange

mi "ฮ่าฮ่าฮ่าฮ่า~! ใช่ นายนี่แหละคือฮิจังเลย!"

hi "ทำไมถึงคิดงั้นกัน…"

shi "…"    

"ฮากามิจิใช้นิ้วเคาะที่โต๊ะเพื่อให้มิช่าหัน ทั้งสองคนทำท่าไปมาอย่างตื่นเต้นจนดูแทบไม่ทัน"

show shizu adjust_happy 
with charachange

show misha sign_smile 
with charachange

show shizu behind_smile 
with charachange

show misha perky_confused 
with charachange

"มิช่าดูจะรับข้อมูลมากไม่ไหว"

show misha hips_grin 
with charachange

mi "อะฮ่าฮ่า~! เอ่อ ขอโทษทีนะ!"

show misha hips_smile 
with charachange

mi "ชิจังจะบอกว่าตัวเองน่ะเป็นหัวหน้าห้องนะ ถ้าอยากรู้อะไรก็ถามได้ไม่ต้องเกรงใจ"

show shizu behind_blank 
with charachange

shi "…"

show misha sign_smile
with charachange

mi "มาวันแรกเป็นไงบ้าง ถ้าไม่มีเวลาเดินดูให้ทั่วก็ให้พวกเราพาไปเดินให้นาย…{w=0.5}{nw}"

show misha perky_smile:
    "misha perky_confused" with Dissolve(0.5, alpha=True)
with None

extend "คุ้นชิน?{w=0.5}{nw}"

show misha perky_confused:
    "misha perky_smile" with Dissolve(0.5, alpha=True)
with None

extend "กับที่นี่ได้นะ!"

"มิช่าชะงักไปนิดหน่อยกับคำยากนั้น ทำให้ที่แปลมาลื่น ๆ ปกติดูสะดุด"

hi "ขอบใจ ช่วยได้เยอะเลย อื้อ วันนี้มาถึงก็มาเรียนเลย"

show shizu behind_frown 
with charachange

shi "…"

show misha hips_laugh 
with charachange

mi "ฮ่าฮ่าฮ่า~!"

show misha hips_smile 
with charachange

mi "ไม่ได้นะ! ปกติจะไปไหนต้องหาข้อมูลที่นั่นมาก่อนสิ แล้วก็ไม่ใช่แค่โรงเรียนด้วย~!"

show misha hips_grin 
with charachange

mi "ต้องรู้มาก่อน! ต่อให้จะไปแค่ร้านสะดวกซื้อก็เถอะ! เอาจริงปะเนี่ยชิจัง ฮ่าฮ่าฮ่า~!"

show misha perky_smile 
show shizu behind_smile 
with charachange

"รู้มาก่อนเหรอ ฉันคงไม่ได้มีอารมณ์อยากจะทำขนาดนั้นละมั้ง"

"ไม่ได้อยากมาเลย ถึงจะลากให้ร่างมาอยู่ได้แล้วก็เถอะ แต่ก็นะ"

"ฉันไม่ได้พูดอะไร ส่วนมิช่าก็ทำภาษามือแล้วยักไหล่ตาม คุยอะไรกัน เรื่องฉันมั้ง"

"อยากจะไหลลงไปจากเก้าอี้ ทั้งสองคนยิ้ม แต่พอฉันเห็นท่ายักไหล่นั้นแล้วก็รู้สึกบางอย่าง"

show misha perky_sad 
with charachange

mi "ดูไม่ร่าเริงเลยนะ ไหวมั้ยเนี่ย"

show shizu basic_normal 
with charachange

shi "…"

show misha perky_smile 
with charachange

mi "อย่าเข้าใจผิดสิ~! พอเห็นคนไม่กล้าถามแล้วไม่ชอบเลย! คนเราต้องถามสิถึงจะรู้~!"

mi "จะขอความช่วยเหลือหรือถามอะไรน่ะไม่แปลกเลย! เลิกทำหน้าเหมือนสอบตกมาได้แล้วน่า!"

show misha cross_laugh 
with charachange

mi "วะฮ่าฮ่าฮ่า~!"

hi "โอเค"

show shizu adjust_happy 
with charachange

shi "…"

show misha perky_smile 
with charachange

mi "อ้อ อีกอย่าง ไม่ต้องเรียกชิจังให้เป็นทางการว่า “ฮากามิจิ” หรือ “หัวหน้าห้อง” ตลอดหรอก! เรียกชิจังก็พอแล้ว~!"

stop music fadeout 0.5

show shizu adjust_blush 
with charachange

shi "…"

show misha hips_smile 
with charachange

mi "อะฮ่าฮ่า~! โอเค อันนั้นก็สนิทเกินไป งั้นเรียก “ชิซูเนะ” ดีกว่าเนอะ"

show shizu basic_normal2 
with charachange

shi "…"

show misha hips_grin 
with charachange

play music music_shizune fadein 5.0

mi "อื้ม ๆ ~! “ชิซูเนะ” ก็ได้!"

hi "ฮะ ๆ ได้ ก็เรียกง่ายดี"

"ค่อยโล่งขึ้นมาหน่อย ทั้งคู่ก็ดูเป็นมิตรดี ทำเอาที่ระแวงเมื่อกี้เหมือนเป็นบ้าไปเองเลย\nยิ่งชิซูเนะที่นึกว่าจะเป็นพวกเอาจริงเอาจังอีก"

"ก็ยังดูจริงจังแหละ แต่ก็ไม่ได้ขนาดนั้นมั้ง"

show shizu behind_frown 
with charachange
    
shi "…!"

show misha perky_confused 
with charachange

mi "หืม อ้อ จริงสิ ยังไม่ได้ทำงานเลย! ต้องรีบทำแล้ว เดี๋ยวชิจังจะโกรธ"

hi "งานก็เยอะด้วย ต้องทำแต่เนิ่น ๆ ถ้าอยากให้เสร็จก่อนเลิกคาบ"

show misha hips_laugh 
with charachange

mi "วะฮ่าฮ่า~! เธอพูดถูก!"

show shizu basic_frown 
with charachange

shi "…"

"ชิซูเนะมองมาทางเราสองคนด้วยความร้อนรน อันนี้ไม่ต้องใช้ภาษามือก็รู้"

hi "โอเค ๆ เข้าใจละ"

show shizu basic_normal 
with charachange

shi "…"

show misha perky_smile 
with charachange

mi "เลิกเรียนแล้วไปเดินในโรงเรียนด้วยกันนะ วันนี้วันดี! โอเคนะ~"

"งานก็ทำยาก ทั้งยากแล้วก็เยอะด้วย"

stop music fadeout 6.0

scene bg school_scienceroom
with shorttimeskip

"ทั้งที่เริ่มช้าแต่ก็เสร็จก่อนคนอื่นไม่กี่นาทีอยู่ดี ชิซูเนะกับมิช่านี่เก่งจริง ๆ"

"แต่ทั้งสองคนก็ต่างกัน หัวห้องดูจะใจเย็นและเรียบร้อยเหมือนหน้าตาท่าทาง ส่วนมิช่าดูขี้เล่น\nกับมีความเป็นผู้หญิงมากกว่า แถมยังสมาธิหลุดง่ายด้วย"

"เอาจริง ๆ งานส่วนใหญ่คือทั้งคู่นั่นแหละที่ทำ รู้สึกผิดเลยแฮะ"

play sound sfx_normalbell

"เสียงระฆังจากหอนาฬิกาดังเป็นสัญญาณว่าหมดคาบและพักเที่ยงแล้ว"

scene bg school_hallway3
with locationchange

"ฉันตามมิช่าที่กวักมือให้เดินไปที่โถงแล้วลงบันไดไปเพราะไม่รู้จะทำอะไรดี"


    
#**************************************
label th_A3:

scene bg school_staircase2
with locationchange

"พวกเราลงมาที่โถงชั้นล่างที่ฉันเพิ่งเจอกับมุโต้เมื่อเช้า"

play ambient sfx_crowd_indoors fadein 6.0

scene bg school_cafeteria
with locationchange

"ในโรงอาหารดูกว้างขวางและทันสมัยขัดกับภายนอกที่ดูเก่าแก่เหมือนตึกอื่น ๆ ในโรงเรียน"

"หน้าต่างบานกว้างเปิดไปทางสนาม ทิศเดียวกับประตูทางเข้า"

show misha hips_grin
with charachange

play music music_ease

mi "นี่โรงอาหาร~!"

"เธอพูดประโยคที่รู้ ๆ กันอยู่แล้วนั้นด้วยความตื่นเต้น คนในโรงอาหารรอบ ๆ ก็มอง แต่มิช่าดูไม่ได้สนใจ\nพวกเราจึงไปต่อแถว"

hide misha
with charaexit

"รายการอาหารค่อนข้างเยอะ ซึ่งดูเหมือนจะเป็นเรื่องที่ดี จนนึกได้ว่าที่มีเยอะเพราะต้องปรับให้เข้ากับนักเรียนบางคน\nที่ต้องคุมอาหารพิเศษ"

"ดีจังเลยนะ เหมือนตอนที่อยู่โรงพยาบาลแล้วต้องกินอาหารให้ถูกสัดส่วนที่วัดมาแล้วเพื่อให้ตรงกับ\nความต้องการของคนไข้"

"ฉันเลือกอย่างหนึ่งมามั่ว ๆ แล้วตามชิซูเนะไปที่โต๊ะแล้วนั่งลงตรงข้ามกับเธอ"

show misha hips_frown at twoleft
show shizu basic_normal at tworight
with charaenter

"ระหว่างที่ละเลียดอยู่ด้วยความไม่สนใจอะไร มิช่าก็สะกิดที่สีข้างให้หันแล้วชี้ไปที่ชิซูเนะ"

show misha perky_smile 
show shizu basic_frown 
with charachange

shi "…"

"ฉันไม่เข้าใจเพราะอ่านภาษามือไม่ออก"

"เวลา “คุย” กับใครต้องมองหน้าเขาไม่ให้เสียมารยาทหรือเปล่านะ"

show misha hips_smile 
show shizu behind_blank 
with charachange

mi "มีอะไรจะถามมั้ย"

hi "หา?"

show misha hips_grin 
with charachange

mi "อะไรก็ได้! พวกเราเป็นไกด์ของนายนะ เพราะงั้นถ้าอยากรู้อะไรก็ถามมาเลย~!"

label th_choiceA3:
menu:
    with menueffect
    
    hi "อืมม นั่นสินะ…"
    
    "ถามเรื่องห้องสมุด":
        return m1
    
    "ถามเรื่องที่ชิซูเนะหูหนวก":
        return m2
    
    "น่าจะรู้พอแล้วละมั้ง":
        return m3        
    
label th_A3a:

hi "เอ้อ จริงด้วย ที่โรงเรียนนี้มีห้องสมุดมั้ย ช่วงนี้อ่านหนังสือเลยอยากลองแวะไปดูน่ะ"

"มิช่าขมวดคิ้วจนรู้เลยว่าเธอคงไม่คิดว่าการอ่านหนังสือเป็นงานอดิเรกที่ดีต่อสุขภาพสักเท่าไหร่\nแล้วก็กลับมายิ้มเหมือนเดิม"

mi "มีสิ~! อยู่ชั้นสองน่ะ ไว้เดี๋ยวพาไปนะ!"

hi "ขอบใจ"

"ฉันกลับมากินต่อแล้วปล่อยให้สองสาวคุยกัน"



label th_A3b:

"พอเห็นชิซูเนะแล้วก็รู้สึกสนใจขึ้นมา อยากจะถามอะไรสักอย่างด้วย"

"แต่จะถามอะไรละลาบละล้วงขนาดนั้นก็คงไม่ได้หรอกมั้ง"

"อืมม…"

"แต่จะนึกอะไรอย่างอื่นมาถามก็นึกไม่ออกแล้ว ก็เลยได้แต่กินข้าวต่อแล้วปล่อยให้สองสาวคุยกัน"



label th_A3c:

hi "นึกไม่ออกเลยแฮะ"

show misha sign_smile
with charachange

mi "โห! แสดงว่าเราทำหน้าที่ไกด์ได้ดีเลยสิเนี่ย ใช่มั้ย ๆ ~"
 
"…"
 
hi "เอ่อ… ถ้าเธอว่างั้นละก็นะ"

show misha hips_grin 
with charachange

show shizu behind_smile
with charachange
 
"มิช่ายิ้มแป้น ชิซูเนะก็ยิ้มตามหลังมิช่าแปลให้"
 
"ฉันส่ายหัวให้กับความตื่นเต้นที่ดูออกจะเกินไปสักหน่อยนั่นแล้วกินข้าวต่อ"



label th_A3d:

"มิช่ากับชิซูเนะทำท่าโน่นนี่คุยภาษามือกันพลางเหลือบมองมาทางนี้ แต่มิช่าก็ไม่แปลให้"

"ความลับสาวน้อยอะไรประมาณนั้นมั้ง"

"…"

stop music fadeout 3.0
$ renpy.music.set_volume(1.0, .5, channel="ambient")
stop ambient fadeout 3.0

"ไม่นานฉันก็รู้ตัวว่าแค่ภาษามือใช้ทำลายความเงียบไม่ได้"

scene bg school_scienceroom
with shorttimeskip

play music music_daily fadein 0.5

"พวกเรามาถึงห้องเร็ว แต่ก็มีคนมาก่อนแล้ว"

show hanako emb_downtimid at Transform(xanchor=0.8, xpos=1.0)
with charaenter

"ผู้หญิงผมยาวสีดำคนที่เห็นเมื่อเช้านั่งฟุบโต๊ะอยู่แถวหลังสุด"

show hanako defarms_shock
with vpunch

show hanako defarms_shock at Transform(xanchor=0.6, xpos=1.0)
with charamove

"เธอสะดุ้งเล็กน้อยเมื่อมิช่าเข้าห้องมาด้วยแรงปานช้าง"

"เธอยิ่งทำตัวลีบไปอีก แค่เห็นยังเกร็งแทน เหมือนแค่เห็นพวกเราแล้วจะกลายเป็นหิน"

"ทั้งมิช่าและชิซูเนะก็เหมือนไม่เห็นและไม่ได้สนใจ เดินผ่านเธอไปนั่งที่แล้วเริ่มคุยกัน"

show hanako defarms_shock at offscreenright
with charamove

hide hanako
with None

"ฉันได้แต่สงสัย คิดยาวมาจนนักเรียนทยอยเข้าห้องมา พร้อมครูเป็นคนสุดท้าย"

"การที่จะทำตัวให้ชินกับการอยู่โรงเรียนก็รู้สึกแปลกดี สมองรู้จำได้ว่าเป็นอย่างนี้ แต่ตัวบอกว่าไม่ใช่"

"พอใกล้เลิกคาบก็เริ่มหาวแล้วนั่งนับเวลาที่เหลือ"

"เพิ่งมาวันแรกจะมานั่งเพลียแบบนี้ได้ที่ไหนล่ะ"

"สงสัยเพราะอยู่ติดโรงพยาบาลนานเลยเป็นอย่างนี้ ทั้งตัวทั้งใจมันห่อเหี่ยวไปหมด"

scene bg school_scienceroom
with shorttimeskip

play sound sfx_normalbell

"ไม่นานระฆังก็ดัง"

"เลิกเรียนสักที"

"มิช่ากับชิซูเนะคุยกันอยู่ข้าง ๆ พอหารือกันได้สักพักก็หันมาทางฉัน"

show shizu behind_blank at tworight
show misha perky_smile at twoleft
with charaenter

shi "…"

mi "วันนี้เราคงอยู่พานายไปเดินไม่ได้แล้วละฮิจัง เราต้องรีบแล้ว พอดีงานต้องทำเยอะมาก"

show shizu basic_normal2 
with charachange

shi "…"

mi "แต่ฉันมั่นใจว่าเดี๋ยวนายก็ชินที่เองแหละ"

hi "เอ้อ เดี๋ยว! ครูบอกให้ไปหาพยาบาล ต้องไปที่ไหนเหรอ"

show shizu behind_smile 
show misha hips_grin 
with charachange

mi "อ้าวเหรอ งั้นเดี๋ยวพาไปก่อนก็ได้~!"

mi "ตามมาสิ พยาบาลเขามีตึกแยก ต้องออกไปข้างนอกนะ"    

hide shizu
hide misha
with charaexit

scene bg school_hallway3
with locationchange

"ทั้งสองคนชี้บอกห้องรุ่นพี่คนอื่นที่อยู่ชั้นเดียวกับห้องเราพลางพาฉันเดินไปตามกลุ่มคนลงมาตามบันได"

scene bg school_courtyard
with locationskip

"พอออกมาข้างนอกก็พาเดินไปอีกตึกหนึ่งที่เล็กกว่าที่อยู่ข้าง ๆ โรงเรียน"

"หน้าตาตึกก็ดูคล้าย ๆ กันจนเหมือนว่าเป็นส่วนหนึ่งของอาคารหลัก"

show shizu behind_smile at tworight
show misha perky_smile at twoleft
with charaenter

shi "…"

mi "ที่นี่คืออาคารรอง ข้างในมีสำนักงานแล้วก็อะไร ๆ สำคัญอยู่เต็มไปหมด อย่างสำนักงานมูลนิธิยามากุ\nห้องทำงานของทีมพยาบาล สระว่ายน้ำยังมีเลย!"

hi "แล้วทำไมสระน้ำมาอยู่ที่สำนักงาน"

show shizu behind_frustrated 
with charachange

shi "…"

show misha hips_grin 
with charachange

mi "ถามอะไรแปลก ๆ ฮิจัง! เอาไว้กายภาพบำบัดไง"

show misha sign_smile 
with charachange

mi "แต่ก็นั่นแหละ สำนักงานทีมพยาบาลอะไรอยู่ในนั้นหมด ส่วนห้องหัวหน้าพยาบาลอยู่ที่ชั้นแรก"

show misha hips_smile 
show shizu behind_smile 
with charachange

mi "ที่เหลือไปเองได้ใช่มั้ย~ งั้นไปก่อนนะ! เจอกันพรุ่งนี้!"

hi "อื้อ ขอบใจ บาย"

stop music fadeout 5.0

hide shizu
hide misha
with charaexit

"ตึกทั้งตึกที่มีไว้สำหรับอย่างอื่นที่ไม่ใช่การเรียนเหรอ"

"ที่แบบนี้ก็คงต้องมีละมั้ง"

scene bg school_nursehall
with locationchange

"ฉันเดินเข้าไป ในใจก็หวังว่าจะแค่แวบเดียวเหมือนที่ครูบอก"

"บนประตูสีขาวมีเครื่องหมายกากบาทสีเขียวอยู่กับข้อความ “หัวหน้าพยาบาล” พร้อมป้ายชื่อ"

play sound sfx_doorknock2

"พอเคาะประตูก็มีเสียงตอบกลับมา แต่ฟังไม่ค่อยรู้เรื่องว่าพูดอะไรอยู่"

"เหมือนจะบอกให้เปิดประตู ก็เลยเปิดประตูเดินเข้าไป"

play sound sfx_dooropen

scene bg school_nurseoffice
with locationchange

"ขนาดของห้องไม่ได้ใหญ่ แถมมีกลิ่นแปลก ๆ ชายที่ดูเป็นมิตรหมุนเก้าอี้หันมาทันทีที่เดินเข้ามา"

"โต๊ะของเขาเป็นระเบียบเรียบร้อยดี แต่ถังขยะใต้โต๊ะมีแต่อุปกรณ์อะไรที่ใช้แล้วจนล้น\nบนโต๊ะก็มีรอยแก้วกาแฟอยู่หลายวง"

play music music_nurse fadein 0.5

show nurse neutral at center
with charaenter

nk_ "สวัสดี มีอะไรให้ช่วยเหรอ"

"ยังดูหนุ่ม ๆ แถมห้าว ๆ ทว่ารอยลักยิ้มก็ทำให้ความห้าวหายไป"

hi "เอ่อ คุณพยาบาลใช่มั้ยครับ"

show nurse grin
with charachange

"เขายิ้มอย่างกับว่ามีคนถามมาเป็นร้อย ๆ ครั้งแล้ว"

nk_ "ก็ใช่น่ะสิ ที่ประตูก็เขียนแปะไว้นี่"

nk_ "จะเรียกชื่อหรือเรียก “คุณพยาบาล” เหมือนคนอื่นก็ได้"

"ก็ได้ ฉันไม่คิดอะไรอีกแล้วจับมือที่เขายื่นออกมารอ{w} เป็นการจับมือที่หนักแน่นและเป็นมิตรดี"

hi "เอ้อ… เอ่อ ผมเพิ่งมาใหม่แล้วครูประจำชั้นบอกให้มาหาคุณ ผมชื่อฮิซาโอะ นากาอินะครับ"

play sound sfx_snap

"เขาทำหน้าร้องอ๋อแล้วดีดนิ้วดังเป๊าะ"

show nurse fabulous
with charachange

nk "อ้อ นากาอิ{b}คนนั้น{/b}น่ะเอง เมื่อเช้าอ่านเอกสารของเธออยู่"

nk "หัวใจเต้นผิดจังหวะเรื้อรังกับหัวใจพิการแต่กำเนิด ประมาณนั้นใช่มั้ย"

"เขาบุ้ยใบ้ให้นั่งที่เก้าอี้ว่างตรงหน้าโต๊ะเขา"

hi "อ่า ครับ"

show nurse neutral
with charachange

nk "ดี เอาละ เธอน่าจะรู้เรื่องโรงเรียนมาพอสมควรแล้ว จะสรุปให้เร็ว ๆ นะ"

nk "ที่นี่มีอะไรทุกอย่างให้ครบครัน ส่วนใหญ่ก็พวกสำหรับกายภาพบำบัดอะไรแบบนั้น"

nk "แล้วก็จะมีทีมพยาบาลคอยอยู่โยงตลอด ตอนกลางคืนก็ด้วย มีปัญหาอะไรก็โทร. มาได้"

"ทีมพยาบาลยี่สิบสี่ชั่วโมงอันขึ้นชื่อ"

hi "โห อย่างกับโรงพยาบาลแน่ะ"

nk "ก็ไม่ใช่เสียทีเดียวหรอก เช่นว่า ที่นี่เราไม่รับผ่าตัดสมองไง"

"มุกที่เขายิงมารู้สึกไม่เข้าจนนึกในใจว่าจะพูดทำไม"

hi "งั้นมั้งครับ… แค่ว่าแปลก ๆ หน่อยที่เป็นโรงเรียนแต่กลับมีหมอมีอะไรแบบนี้เยอะแยะเลย"

nk "เดี๋ยวก็ชินน่า"

"ฉันไม่แน่ใจว่าจะชินได้จริงมั้ย แต่ก็ไม่ได้บอกเขาไป"

nk "เอาละ ขอหาเอกสารเธอแป๊บนะ…"

"ระหว่างที่เขากำลังเปิดคอมหานู่นนี่กับค้นกองเอกสารฉันก็กวาดตามองรอบ ๆ ห้อง"

"เป็นห้องที่แสนธรรมดาที่สุดเลยก็ว่าได้"

"ผนังกับฝ้าสีเบจ พื้นลามิเนตสีเทา แล้วก็ของที่มีตามห้องพยาบาลของโรงเรียน"

"ตามกำแพงสี่ด้านยังมีโปสเตอร์ให้ความรู้ที่เห็นแล้วก็ตลกดี ที่บอกให้กินอาหารให้ครบสามมื้อห้าหมู่อะไรเทือกนั้น"

show nurse grin
with charachange

"คุณพยาบาลยิ้มพลางดึงเอกสารหนาปึ้กออกมาจากกองเอกสารพะเนินแล้วเปิดออกมา"

nk "อื้ม หมอของเธอก็สั่งยารักษาโรคหัวใจเต้นผิดจังหวะมาให้แล้ว พยายามกินให้ครบทุกเช้าเย็นด้วยละ\nเดี๋ยวยาจะไม่ออกฤทธิ์เต็มที่"

show nurse fabulous
with charachange

nk "นอกนั้นก็… เธอเล่นกีฬามั้ย ที่ออกแรงมาก อย่าง… ไม่รู้สิ ชกมวยงี้"

"เขาหยักยิ้มกับมุกของตัวเอง แต่ฉันไม่ยิ้มด้วย"

hi "เอ่อ ก็ มีเล่นเตะบอลกับคนในห้องบ้างครับ"

nk "โอเค งั้นฉันต้องขอให้เธองดนะ อย่างน้อยก็สักพักหนึ่ง"

hi "อ้อ ครับ"

"ท่าที่ที่ไม่แยแสอะไรของฉันทำให้เขายักคิ้ว เอาจริง ๆ ก็ไม่ได้อะไรที่ห้ามไปเตะบอลเล่นหรอก"

"คงไม่ได้เล่นเพราะมุ่งมั่นในกีฬาแหละมั้ง แค่ไม่มีอะไรทำเฉย ๆ"

nk "แล้วก็ถ้ามีอะไรมากระทบกระเทือนอีกก็อาจเป็นอันตรายต่อหัวใจเธอได้ อย่าไปทำอะไรสุ่มเสี่ยงให้โดนอีกรอบแล้วกัน"

nk "ที่เป็นล่าสุดนี่คือมีอะไรมากระทบกระเทือนที่หน้าอกเหรอ ในเอกสารไม่เห็นมีบอกอะไรเลย"

hi "เอ้อ… ไม่เชิงครับ"

show nurse concern
with charachange

"ฉันพยายามเลี่ยงคำถามเนียน ๆ เขาเหลือบมองฉันสลับกับเอกสาร หน้าตาดูเคร่งเครียดขึ้น"

nk "แต่ยังไงรักษาสุขภาพไว้ก็ดี ออกกำลังกายหน่อยก็ได้"

nk "อย่างที่บอกนั่นแหละว่ามีกายภาพบำบัดให้ด้วย แต่สำหรับเธอคงไม่ต้องถึงขั้นนั้น"

nk "แค่ออกกำลังกายเบา ๆ ก็พอ"

nk "จะเดินเร็ว วิ่งจ็อกกิงหน่อย โดดเชือก อะไรเทือกนั้นน่ะ ว่ายน้ำ ก็ได้แหละมั้ง ที่นี่มีสระด้วยนะ"

hi "มีคนบอกอยู่ครับ"

show nurse neutral
with charachange

nk "เหรอ ดี ๆ "

nk "นั่นแหละ ถึงอาจจะมีคนบอกเธอแล้ว แต่ก็อย่าไปหักโหมร่างกายตัวเองให้มาก"

"เขาส่ายนิ้วไปมาเน้นย้ำ ไม่ต้องก็ได้หรอก คือฟังมาเป็นพันรอบแล้ว"

show nurse concern
with charachange

nk "ห้ามเสี่ยงอะไรเด็ดขาด ดูแลตัวเองด้วย"

hi "โอเคครับ"

"เขามองเอกสารอีกครั้งแล้ววางไว้บนโต๊ะด้วยสีหน้าที่พอใจ"

show nurse neutral
with charachange

nk "ดี งั้นก็แค่นี้แหละ มีอะไรก็มาหานะ"

scene bg school_nursehall
with locationchange

stop music fadeout 2.0

"รู้ตัวอีกทีก็มาอยู่นอกห้องแล้ว แวบเดียวจริง ๆ"


#***************************************

label th_A4:

scene bg school_courtyard
with locationchange

play music music_pearly fadein 4.0

"ฉันมายืนอยู่ที่หน้าอาคารหลักกับอาคารรอง ถึงฉันจะว่าทั้งคู่ก็ดูเหมือน ๆ กันก็เถอะ"

"เป็นครั้งแรกที่ได้เห็นนักเรียนคนอื่น ๆ เลยมามองคนที่เดินออกจากอาคารมาไปที่ประตูไม่ก็หอ"

"ทุกคนดูมีเป้าหมายที่จะไป"

"ส่วนฉันก็ยังคิดอยู่ว่าส่วนใหญ่ก็ดูไม่พิเศษขนาดนั้นกับโรงเรียนพิเศษนี้ แต่ก็ใช่ว่าฉันจะดูพิเศษน่ะนะ"

"แสดงว่าฉันก็อยู่กลุ่มเดียวกับพวกเขา หรือ{b}พวกเรา{/b}งั้นสิ"

"…"

"ฉันก็ไปบ้างดีกว่า จะได้ไม่มาเหม่อลอยชายอยู่แบบนี้"

"จะถึงเวลาข้าวเย็นแล้ว แต่รู้สึกเหนื่อยมากกว่าหิว"

"ยิ่งเดินไปหอที่อยู่ไม่ห่างจากบริเวณโรงเรียนก็ยิ่งล้าไปอีก"

scene bg school_gardens
with locationchange

"ระหว่างทางจะมีสวนอยู่ด้วย ทั้งพุ่มไม้ ดอกไม้ กลิ่นหญ้าเหม็นเขียวที่ตัดใหม่คอยอยู่เป็นบรรยากาศ"

"กลิ่นเตะจมูกขึ้นสมองเตือนว่าเป็นความรู้สึกใหม่เพราะไม่ได้ออกมาข้างนอกนานแล้ว"

scene bg school_dormext_start at bgleft
with locationchange

"หอพักเป็นตึกอิฐแดงขนาดใหญ่ ซึ่งก็ดูหรูหราเกินที่หน้าที่เหมือนตึกอื่น ๆ ด้วยนั่นแหละ พอมาถึงฉันก็เดินเข้าไป"

scene bg school_dormhallground
with locationchange

"ฉันยืนควานหากุญแจในกระเป๋ากางเกงอยู่เสียนาน"

hi "ห้อง หนึ่ง หนึ่ง เก้า…"

"แม้ภายนอกจะตกแต่งดูดี ภายในยังดูใหม่ สมการใช้งาน และน่าเบื่อ"

"โถงทางเดินกับประตูทำมากว้างไว้สำหรับวีลแชร์เหมือนอาคารหลัก ทั้งลิฟต์ที่อยู่สุดทางเดินก็ด้วย"

"ฉันชะโงกหน้าเข้าไปดูตรงประตูห้องรวมของหอ"

"ในห้องมีคนสองสามคนดูทีวีอยู่"

"คนหนึ่งหันมาทักทาย “สวัสดี” แล้วหันไปดูทีวีต่อ"

"ที่นี่คงมีแค่พวกผู้หญิงแหละมั้งที่เป็นมิตร ก็โอเคดีละมั้ง"

"ฉันเดินขึ้นบันไดไปชั้นบน"

scene bg school_dormhallway
with locationchange

"ตรงนี้มีทางเดินแยกออกไปจากโถงทางเดินหลักด้วย"

"แต่ละทางที่แยกออกไปเหมือนจะเป็นห้องน้ำกับห้องอาบน้ำ แล้วก็มีห้องอีกสี่ห้อง"

"ฉันตามหาห้อง 119 เมื่อเดินไปได้ครึ่งทาง"

"ป้ายชื่อที่อยู่ห้องซ้ายขวากันนั้นว่าง สงสัยทั้งชั้นจะเหลือแค่ฉันกับอีกห้อง"

play sound sfx_doorknock2
stop music fadeout 3.0

"มีแสงลอดออกมาจากพื้นประตูห้อง 117 ฉันจึงเคาะประตูไป"

hi "สวัสดีครับ มีใครอยู่มั้ย"

"ข้างในมีเสียงกุกกัก แล้วก็เสียงปลดล็อกหลายรอบที่ดูจะมากเกินสำหรับประตูแบบนี้ สักพักหนึ่งประตูก็เปิดออก"

show kenji tsun at Slide(0.4,0.5,0.5,0.5,0.5)
with charaenter

play music music_kenji fadein 0.5

"ชายใส่แว่นคนหนึ่งที่ยืนอยู่ตรงประตูจ้องเขม็งมาผ่านแว่นที่หนาเตอะ"

ke_ "ใครน่ะ"

"ตาบอดเหรอ ไม่สิ อย่างน้อยก็คงไม่บอดหมด งั้นก็คงไม่ใส่แว่นหรอก"

show kenji tsun_close at center
with characlose

"เขาโน้มตัวเข้ามาจนจมูกพวกเราแทบแตะกัน ปากก็เหม็นกระเทียม"

hi "ฉันฮิซาโอะ นากาอิ… อยู่ห้องตรงนั้น พอดีเพิ่งย้ายมาเลยกะจะแวะมาทัก…"

show kenji happy
with charadistant

"เขาทำหน้าร้องอ๋อขึ้นมาแล้วยืดตัวตรง ยิ้มทักทายยื่นมือออกมาแทบจะทิ่มลิ้นปี่ฉัน"

ke_ "อ้อ ไงพวก ฉันเคนจิ"

hi "เอ่อ หวัดดี"

"ฉันจับมือเคนจิทักทาย ยังรู้สึกกลัว ๆ กับท่าทีที่เปลี่ยนมาเป็นการต้อนรับขับไล่ขนาดนี้"

ke "เมื่อกี้มีคนที่ดูน่าสงสัยมาห้องนายแน่ะ"

hi "พ่อแม่ฉันละมั้ง"

show kenji tsun
with charachange

ke "พ่อแม่นายเหรอ แน่ใจนะ อาจจะเป็นใครไม่รู้ก็ได้ คนเรารู้หน้าไม่รู้ใจหรอก"

"สำนวนที่อยู่ ๆ ก็โผล่มานั้นค้างอยู่ในอากาศ ฉันนึกหาคำพูดเพื่อต่อบทสนทนา"

hi "ก็มีโอกาสสูงพอที่จะเป็นพ่อแม่ฉันน่ะแหละ"

"เขาผวาไปและโบกไม้โบกมือไปมาดูเวอร์ ๆ "

show kenji neutral
with charachange

ke "นายช่างกล้าหาญเสียจริง ฮิซาโอะ"

show kenji tsun
with charachange

ke "ฉันน่ะ ไม่วางใจในโอกาสอะไรแบบนั้นหรอก"

ke "สิ่งที่ฉันวางใจเชื่ออย่างเดียวคือตัวเอง"

hi "แปลว่าฉันไม่ควรเป็นเพื่อนนายด้วยงั้นสิ"

"เขาคิดอยู่พักใหญ่"

show kenji neutral
with charachange

ke "ก็เป็นทางเลือกที่ฉลาดดี"

show kenji happy
with charachange

ke "โห นายนี่ฉลาดกว่าที่เห็นนะเนี่ย"

ke "มั้งนะ"

ke "หน้าตานายเป็นไง หวังว่าจะไม่ฉลาดนะ"

show kenji neutral_close
with characlose

"เขาหรี่ตาแล้วยื่นหน้าเข้ามาอีก แต่ฉันเอนตัวหลบ"

show kenji tsun
with charadistant

ke "ช่างเหอะ ไม่สำคัญ"

hide kenji
with charaexit

stop music fadeout 3.0

"พูดจบเขาก็กลับไปคลำหากลอนประตูอยู่พักหนึ่ง{w=0.3}{nw}"

play sound sfx_doorslam

extend "แล้วปิดประตู"

"ฉันสอดกุญแจเข้ากับประตูที่ติดหมายเลข 119"

scene bg school_dormhisao_ss
with locationchange

play music music_night fadein 1.0

"กำแพงสีเบจเปล่า ๆ ผ้าสีขาว โต๊ะไม้น้ำหนักเบา ผ้าม่านน่าเกลียด ๆ "

"เป็นห้องที่กลาง ๆ ไม่มีความเป็นตัวตนเลย เหมือนห้องฉันที่โรงพยาบาล"

"กระเป๋าฉันวางอยู่ที่ปลายเตียง ดูแฟบลงกว่าเมื่อเช้านี้"

"ตู้เสื้อผ้าที่มีเสื้อผ้าของฉันเปิดอยู่"

"เหมือนจะมีชุดนักเรียนแขวนอยู่สองสามตัวด้วย"

"ที่แขนเสื้อตัวหนึ่งมีโน้ตติดไว้"

window hide

$ written_note("ไง ฮิจัง พ่อแม่จัดของกับจัดเตียงให้แล้วนะ เขาบอกมาว่าถ้าชุดคับหรือหลวมไป\nก็ไปเปลี่ยนที่สำนักงานพรุ่งนี้ได้ มีปัญหาอะไรก็โทร. หาได้เสมอเลยนะ \n\nรักมาก พ่อ แม่")


window show

"เอาละ อย่างน้อยก็ไม่ต้องลำบากจัดของแล้ว"

"จริง ๆ ก็อยากจัดเพราะไม่มีอะไรทำเหมือนกัน"

"ตอนนี้ยังไม่ค่ำมาก"

"ฉันวางโน้ตไว้ที่โต๊ะแล้วนอนบนเตียงด้วยความเหนื่อยอ่อน"

"นอนแล้วก็อยากอ่านอะไรสักอย่าง แต่ไม่ได้เอาอะไรมาเลย"

"ไม่รู้ว่าเป็นเพราะอยู่ที่โรงพยาบาลจนติดนิสัยอยากอ่านอะไรสักอย่างตอนไม่มีอะไรทำหรือเปล่า"    

"เริ่มรู้สึกว้าวุ่นมากขึ้นเรื่อย ๆ จนต้องลุกขึ้น"

"อาจจะเครียด"

"ฉันคิดมากตั้งแต่ก่อนมาแล้ว แถมตอนอยู่ที่โรงเรียนก็ด้วย คงยังไม่หายมั้ง"

"โอ๊ย ต้องหาอะไรทำไม่ให้คิดมากสักหน่อย จะได้ไม่เกร็ง"

"พรุ่งนี้จะไปยืมหนังสือจากห้องสมุดมาอ่านสักหน่อย"

"โอเค ตามนั้น"

"ส่วนตอนนี้…"

show pills:
    alpha 0.0 xalign 0.5 yanchor 0.5 ypos 0.7 subpixel True
    easein 1.0 ypos 0.5 alpha 1.0
with Pause (1.0)

"พอหันไปก็สะดุดตาเข้ากับขวดยาที่เรียงอย่างเป็นระเบียบอยู่บนโต๊ะข้างเตียง"

"ฉันหยิบมาขวดหนึ่งแล้วเขย่าฟังเสียงด้านในดู จากนั้นก็อ่านฉลากยา"

window hide

show pills:
    alpha 1.0 xalign 0.5 yanchor 0.5 ypos 0.5 subpixel True
    easeout 1.0 ypos 0.7 alpha 0.0
with Pause (1.0)

hide pills
with None

$ written_note("ฮิซาโอะ นากาอิ\n\nวันละสองเม็ดกันตาย", quiet=True)

window show

"ไม่ได้เขียนว่างั้นหรอก แต่ก็ไม่ต่างอะไรมาก"

"นึกแล้วก็สยองดี ทั้งชีวิตขึ้นอยู่กับยาอะไรแบบนี้ ไม่ค่อยชอบเท่าไหร่ แต่ก็ใช่ว่าจะมีทางเลือกละนะ"

"พอถอนหายใจเสร็จก็ทำกิจวัตรการกินยาให้ถูกขนาดจากแต่ละขวด ต้องคอยดูให้ดีด้วยว่าใช่หรือยัง"

"…"

"และกลับมานอนอีกครั้งด้วยความรู้สึกว่างโหวงกับไม่มั่นคง เอาแต่จ้องเพดานเปล่าที่ไม่คุ้นตาอยู่นาน"

stop music fadeout 8.0

scene bg school_dormhisao_ni
with Dissolve(3.0)

"แต่จ้องไปก็ไม่คุ้นตาขึ้นมาเลย แม้ความมืดและเงาดำจะเข้าปกคลุมห้องฉันอย่างนิ้วที่คืบคลานแล้วก็ตาม"

"ส่วนที่นอนก็เริ่มนอนสบายขึ้น ความอบอุ่นห่มตัวให้พ้นจากความเย็นที่ไม่ไกลจากอุณหภูมิห้องนัก"

"ไม่นานแสงสลัวบนเพดานที่เหมือน ๆ กับเพดานทุกที่ตอนกลางคืนก็เป็นสิ่งสุดท้ายที่ฉันจดจำได้"

"ราตรีเชื้อเชิญสู่นิทรา ความเย็นเยือกของความไม่คุ้นชินคืบคลานเข้าสู่ประสาท"

"ฉันร่วงหล่นไปไกลจากโลกที่ฉันรู้จัก"    

$ suppress_window_after_timeskip = True

scene black
with shuteye


return

